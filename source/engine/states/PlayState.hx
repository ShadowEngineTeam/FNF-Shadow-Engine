package states;

import backend.EaseUtil;
import backend.Highscore;
import backend.StageData;
import backend.WeekData;
import backend.Song;
import backend.Section;
import backend.Rating;
import backend.scripting.ScriptResult;
import flixel.FlxBasic;
import flixel.FlxObject;
import flixel.addons.transition.FlxTransitionableState;
import flixel.util.FlxSort;
import flixel.input.keyboard.FlxKey;
import flixel.animation.FlxAnimationController;
import openfl.utils.Assets;
import openfl.events.KeyboardEvent;
import cutscenes.CutsceneHandler;
import cutscenes.DialogueBoxPsych;
import states.editors.CharacterEditorState;
import states.editors.ChartingState;
import substates.PauseSubState;
import substates.GameOverSubstate;
import flixel.addons.display.FlxRuntimeShader;
import objects.Note.EventNote;
import objects.*;
import states.stages.objects.*;
import substates.ResultsScreen;
import effects.RetroCameraFade;

#if (target.threaded)
import sys.thread.Thread;
import sys.thread.Mutex;
#end

/**
 * This is where all the Gameplay stuff happens and is managed
 *
 * here's some useful tips if you are making a mod in source:
 *
 * If you want to add your stage to the game, copy states/stages/Template.hx,
 * and put your stage code there, then, on PlayState, search for
 * "switch (curStage)", and add your stage to that list.
 *
 * If you want to code Events, you can either code it on a Stage file or on PlayState, if you're doing the latter, search for:
 *
 * "function eventPushed" - Only called *one time* when the game loads, use it for precaching events that use the same assets, no matter the values
 * "function eventPushedUnique" - Called one time per event, use it for precaching events that uses different assets based on its values
 * "function eventEarlyTrigger" - Used for making your event start a few MILLISECONDS earlier
 * "function triggerEvent" - Called when the song hits your event's timestamp, this is probably what you were looking for
**/
class PlayState extends MusicBeatState
{
	public static final STRUM_X:Float = 48;
	public static final STRUM_X_MIDDLESCROLL:Float = -278;

	public static final ratingStuff:Array<Array<Dynamic>> = [
		['You Suck!', 0.2], // From 0% to 19%
		['Shit', 0.4], // From 20% to 39%
		['Bad', 0.5], // From 40% to 49%
		['Bruh', 0.6], // From 50% to 59%
		['Meh', 0.69], // From 60% to 68%
		['Nice', 0.7], // 69%
		['Good', 0.8], // From 70% to 79%
		['Great', 0.9], // From 80% to 89%
		['Sick!', 1], // From 90% to 99%
		['Perfect!!', 1] // The value on this one isn't used actually, since Perfect is always "1"
	];

	// event variables
	private var isCameraOnForcedPos:Bool = false;

	public var boyfriendMap:Map<String, Character> = new Map<String, Character>();
	public var dadMap:Map<String, Character> = new Map<String, Character>();
	public var gfMap:Map<String, Character> = new Map<String, Character>();

	public var BF_X:Float = 770;
	public var BF_Y:Float = 100;
	public var DAD_X:Float = 100;
	public var DAD_Y:Float = 100;
	public var GF_X:Float = 400;
	public var GF_Y:Float = 130;

	public var songSpeedTween:FlxTween;
	public var songSpeed(default, set):Float = 1;
	public var songSpeedType:String = "multiplicative";
	public var noteKillOffset:Float = 350;

	public var playbackRate(default, set):Float = 1;

	public var boyfriendGroup:FlxSpriteGroup;
	public var dadGroup:FlxSpriteGroup;
	public var gfGroup:FlxSpriteGroup;

	public static var curStage:String = '';
	public static var stageUI:String = "normal";
	public static var isPixelStage(get, never):Bool;

	@:noCompletion
	static function get_isPixelStage():Bool
		return stageUI == "pixel" || stageUI.endsWith("-pixel");

	public static var SONG:SwagSong = null;
	public static var isStoryMode:Bool = false;
	public static var storyWeek:Int = 0;
	public static var storyPlaylist:Array<String> = [];
	public static var storyDifficulty:Int = 1;

	public var spawnTime:Float = 2000;

	public var vocals:FlxSound;
	public var opponentVocals:FlxSound;

	public var intro3Sound:FlxSound;
	public var intro2Sound:FlxSound;
	public var intro1Sound:FlxSound;
	public var introGoSound:FlxSound;
	public var missnoteSound:FlxSound;
	public var hitsoundSound:FlxSound;

	public var dad:Character = null;
	public var gf:Character = null;
	public var boyfriend:Character = null;

	public var notes:FlxTypedGroup<Note>;
	public var unspawnNotes:Array<Note> = [];
	public var eventNotes:Array<EventNote> = [];

	public var camFollow:FlxObject;

	private static var prevCamFollow:FlxObject;

	public var strumLineNotes:FlxTypedGroup<StrumNote>;
	public var opponentStrums:FlxTypedGroup<StrumNote>;
	public var playerStrums:FlxTypedGroup<StrumNote>;
	public var grpNoteSplashes:FlxTypedGroup<NoteSplash>;
	public var grpHoldSplashes:FlxTypedGroup<SustainSplash>;

	public var camZooming:Bool = false;
	public var camZoomingMult:Float = 1;
	public var camZoomingFrequency:Float = 4;
	public var camZoomingDecay:Float = 1;

	public var gfSpeed:Int = 1;
	public var health(default, set):Float = 1;
	public var combo:Int = 0;
	public var maxCombo:Int = 0;

	public var healthBar:Bar;
	public var timeBar:Bar;

	var songPercent:Float = 0;

	public var ratingsData:Array<Rating> = Rating.loadDefault();
	public var isErect:Bool;

	private var generatedMusic:Bool = false;

	public var endingSong:Bool = false;
	public var startingSong:Bool = false;

	private var updateTime:Bool = true;

	public static var changedDifficulty:Bool = false;
	public static var chartingMode:Bool = false;

	// Gameplay settings
	public var healthGain:Float = 1;
	public var healthLoss:Float = 1;

	public var guitarHeroSustains:Bool = false;
	public var instakillOnMiss:Bool = false;
	public var cpuControlled:Bool = false;
	public var practiceMode:Bool = false;

	public var botplaySine:Float = 0;
	public var botplayTxt:FlxText;

	public var iconP1:HealthIcon;
	public var iconP2:HealthIcon;
	public var camHUD:ShadowCamera;
	public var camGame:ShadowCamera;
	public var camOther:ShadowCamera;
	public var cameraSpeed:Float = 1;

	public var songScore:Int = 0;

	public var songHits:Int = 0;
	public var songMisses:Int = 0;
	public var scoreTxt:FlxText;

	var timeTxt:FlxText;
	var scoreTxtTween:FlxTween;

	var zoomTween:FlxTween;
	var camTween:FlxTween;

	public static var campaignScore:Int = 0;
	public static var campaignMisses:Int = 0;
	public static var seenCutscene:Bool = false;
	public static var deathCounter:Int = 0;

	public var defaultCamZoom:Float = 1.05;

	// how big to stretch the pixel art assets
	public static var daPixelZoom:Float = 6;

	private var singAnimations:Array<String> = ['singLEFT', 'singDOWN', 'singUP', 'singRIGHT'];

	public var inCutscene:Bool = false;
	public var skipCountdown:Bool = false;

	var songLength:Float = 0;

	public var boyfriendCameraOffset:Array<Float> = null;
	public var opponentCameraOffset:Array<Float> = null;
	public var girlfriendCameraOffset:Array<Float> = null;

	#if FEATURE_DISCORD_RPC
	// Discord RPC variables
	var storyDifficultyText:String = "";
	var detailsText:String = "";
	var detailsPausedText:String = "";
	#end

	// Lua shi
	public static var instance:PlayState;

	public var introSoundsSuffix:String = '';

	// Less laggy controls
	private var keysArray:Array<String>;

	public var songName:String;

	// Callbacks for stages
	public var startCallback:Void->Void = null;
	public var endCallback:Void->Void = null;

	#if (target.threaded)
	private var shutdownThread:Bool = false;
	private var gameFroze:Bool = false;
	private var requiresSyncing:Bool = false;
	private var lastCorrectSongPos:Float = -1.0;
	private var syncMutex:Mutex;
	#end

	public var noteSkin:String;
	public var noteSkin1:String; // for opponent bleh
	public var allowedNotes:Array<String> = [null, 'Alt Animation', 'No Animation', 'GF Sing', ''];

	// for results screen
	public var totalSick:Int = 0;
	public var totalGood:Int = 0;
	public var totalBad:Int = 0;
	public var totalShit:Int = 0;
	public var anas:Array<Ana> = [null, null, null, null];
	public var anaArray:Array<Ana> = [];

	/**
	 * Stamps the input event sitting in `note`'s lane with the judgement it earned.
	 *
	 * The old inline version rebuilt `nearestNote` as a fresh `[strumTime, noteData, sustainLength]`
	 * array - which, for held sustains and for botplay, meant one array allocation per held note per
	 * frame. Reuse the array the Ana already owns.
	 */
	function updateAna(note:Note):Void
	{
		final ana:Ana = anas[note.noteData];
		if (ana == null)
			return;

		ana.hit = true;
		ana.hitJudge = judgeNote(Math.abs(note.strumTime - Conductor.songPosition + ClientPrefs.data.ratingOffset));

		var nearest:Array<Dynamic> = ana.nearestNote;
		if (nearest == null)
			ana.nearestNote = nearest = [note.strumTime, note.noteData, note.sustainLength];
		else
		{
			nearest[0] = note.strumTime;
			nearest[1] = note.noteData;
			nearest[2] = note.sustainLength;
		}
	}

	/** Moves the pending input event for `direction` into the results log, exactly once. */
	function flushAna(direction:Int):Void
	{
		if (direction < 0 || direction >= anas.length)
			return;

		final ana:Ana = anas[direction];
		if (ana == null)
			return;

		anas[direction] = null;
		anaArray.push(ana);
	}

	public var songSaveNotes:Array<Dynamic> = [];
	public var songJudges:Array<String> = [];

	// from gacha horror recreation
	public var characterPlayingAsDad:Bool = false;

	var dialogueCount:Int = 0;

	public var psychDialogue:DialogueBoxPsych;

	var startTimer:FlxTimer;
	var finishTimer:FlxTimer = null;

	public var countdownReady:FlxSprite;
	public var countdownSet:FlxSprite;
	public var countdownGo:FlxSprite;

	public static var startOnTime:Float = 0;

	var debugNum:Int = 0;
	var noteTypes:Array<String> = [];
	var eventsPushed:Array<String> = [];
	public var totalPlayed:Int = 0;
	public var totalNotesHit:Float = 0.0;

	public var showCombo:Bool = false;
	public var showComboNum:Bool = true;
	public var showRating:Bool = true;
	public var noteTimingRating:FlxText;
	public var noteTimingRatingTween:FlxTween;
	public var comboGroup:FlxTypedGroup<Combo>;
	public var uiGroup:FlxSpriteGroup;
	public var noteGroup:FlxTypedGroup<FlxBasic>;

	var _lastSongScore:Int = -1;
	var _lastSongMisses:Int = -1;
	var _lastSongHits:Int = -1;
	var _lastCombo:Int = -1;
	var _lastTotalPlayed:Int = -1;
	var _lastTotalNotesHit:Float = -1;
	var _lastSecondsTotal:Int = -1;
	var _lastCpuControlled:Null<Bool> = null;
	public var transitioning:Bool = false;
	public var autoUpdateRPC:Bool = true;

	override public function create():Void
	{
		Paths.clearStoredMemory();

		Note.clearPool();

		startCallback = startCountdown;
		endCallback = endSong;

		instance = this;

		PauseSubState.songName = null;
		playbackRate = ClientPrefs.getGameplaySetting('songspeed');
		characterPlayingAsDad = ClientPrefs.getGameplaySetting('playAsOpponent');

		keysArray = ['note_left', 'note_down', 'note_up', 'note_right'];

		FlxG.sound.music?.stop();

		// Gameplay settings
		healthGain = ClientPrefs.getGameplaySetting('healthgain');
		healthLoss = ClientPrefs.getGameplaySetting('healthloss');
		instakillOnMiss = ClientPrefs.getGameplaySetting('instakill');
		practiceMode = ClientPrefs.getGameplaySetting('practice');
		cpuControlled = ClientPrefs.getGameplaySetting('botplay');
		guitarHeroSustains = ClientPrefs.data.guitarHeroSustains;

		camGame = initPsychCamera();

		FlxG.cameras.add(camHUD = new ShadowCamera(), false);
		FlxG.cameras.add(camOther = new ShadowCamera(), false);
		camHUD.bgColor.alpha = camOther.bgColor.alpha = 0;

		grpNoteSplashes = new FlxTypedGroup<NoteSplash>();
		NoteSplash.mainGroup = grpNoteSplashes;

		grpHoldSplashes = new FlxTypedGroup<SustainSplash>();

		persistentUpdate = persistentDraw = true;

		SONG ??= Song.loadFromJson('tutorial');
		Conductor.mapBPMChanges(SONG);
		Conductor.bpm = SONG.bpm;

		noteSkin1 = !characterPlayingAsDad ? SONG.opponentArrowSkin : SONG.playerArrowSkin;
		noteSkin1 ??= Note.defaultNoteSkin;

		noteSkin = !characterPlayingAsDad ? SONG.playerArrowSkin : SONG.opponentArrowSkin;
		noteSkin ??= Note.defaultNoteSkin;

		#if FEATURE_DISCORD_RPC
		storyDifficultyText = Difficulty.diffToString(Difficulty.getByIndex());
		detailsText = isStoryMode ? 'Story Mode: ${WeekData.getCurrentWeek().weekName}' : 'Freeplay';

		// String for when the game is paused
		detailsPausedText = 'Paused - $detailsText';
		#end

		songName = Paths.formatToSongPath(SONG.song);

		if (SONG.stage == null || SONG.stage.length == 0)
			SONG.stage = StageData.vanillaSongStage(songName);

		curStage = SONG.stage;

		// P-Slice compatibility
		if (curStage == "mainStageErect")
			curStage = "stageErect";

		final stageData:StageFile = StageData.getStageFile(curStage) ?? StageData.dummy();

		defaultCamZoom = stageData.defaultZoom;

		stageUI = "normal";

		if (stageData.stageUI?.trim().length > 0)
			stageUI = stageData.stageUI;
		else if (stageData.isPixelStage)
			stageUI = "pixel";

		GameOverSubstate.resetVariables();

		BF_X = stageData.boyfriend[0];
		BF_Y = stageData.boyfriend[1];
		GF_X = stageData.girlfriend[0];
		GF_Y = stageData.girlfriend[1];
		DAD_X = stageData.opponent[0];
		DAD_Y = stageData.opponent[1];

		if (stageData.camera_speed != null)
			cameraSpeed = stageData.camera_speed;

		boyfriendCameraOffset = stageData.camera_boyfriend ?? [0, 0];
		opponentCameraOffset = stageData.camera_opponent ?? [0, 0];
		girlfriendCameraOffset = stageData.camera_girlfriend ?? [0, 0];

		boyfriendGroup = new FlxSpriteGroup(BF_X, BF_Y);
		dadGroup = new FlxSpriteGroup(DAD_X, DAD_Y);
		gfGroup = new FlxSpriteGroup(GF_X, GF_Y);

		switch (curStage)
		{
			case 'stage': new states.stages.Stage();
			case 'stageErect': new states.stages.StageErect();
			case 'nothing': new states.stages.Nothing();
		}

		if (isPixelStage)
			introSoundsSuffix = '-pixel';

		if (stageData.objects?.length > 0)
		{
			final list:Map<String, FlxSprite> = StageData.addObjectsToState(stageData.objects, !stageData.hide_girlfriend ? gfGroup : null, dadGroup, boyfriendGroup, this);
			for (key => spr in list)
			{
				if (!StageData.reservedNames.contains(key))
					modchartSprites.set(key, cast spr);
			}
		}
		else
		{
			add(gfGroup);
			add(dadGroup);
			add(boyfriendGroup);
		}

		// "GLOBAL" SCRIPTS
		#if (FEATURE_LUA || FEATURE_HSCRIPT)
		for (folder in Mods.directoriesWithFile(Paths.getSharedPath(), 'scripts/'))
		{
			for (file in FileSystem.readDirectory(folder))
			{
				#if FEATURE_LUA
				startLuasNamed(folder + file);
				#end

				#if FEATURE_HSCRIPT
				startHScriptsNamed(folder + file);
				#end
			}
		}
		#end

		// STAGE SCRIPTS
		#if FEATURE_LUA
		startLuasNamed('stages/$curStage');
		#end

		#if FEATURE_HSCRIPT
		startHScriptsNamed('stages/$curStage');
		#end

		if (SONG.gfVersion == null || SONG.gfVersion.length == 0)
			SONG.gfVersion = 'gf'; // Fix for the Chart Editor

		gfGroup.add(gf = new Character(0, 0, !stageData.hide_girlfriend && !ClientPrefs.data.lowQuality ? SONG.gfVersion : 'gf-empty'));
		startCharacterPos(gf);
		gf.scrollFactor.set(0.95, 0.95);
		startCharacterScripts(gf.curCharacter);

		dadGroup.add(dad = new Character(0, 0, SONG.player2, characterPlayingAsDad));
		if (characterPlayingAsDad) dad.flipX = !dad.flipX;
		startCharacterPos(dad, true);
		startCharacterScripts(dad.curCharacter);

		boyfriendGroup.add(boyfriend = new Character(0, 0, SONG.player1, !characterPlayingAsDad));
		if (characterPlayingAsDad) boyfriend.flipX = !boyfriend.flipX;
		startCharacterPos(boyfriend);
		startCharacterScripts(boyfriend.curCharacter);

		final camPos:FlxPoint = FlxPoint.get(girlfriendCameraOffset[0], girlfriendCameraOffset[1]);
		if (gf != null)
		{
			camPos.x += gf.getGraphicMidpoint().x + gf.cameraPosition[0];
			camPos.y += gf.getGraphicMidpoint().y + gf.cameraPosition[1];
		}

		if (dad.curCharacter.startsWith('gf'))
		{
			dad.setPosition(GF_X, GF_Y);
			if (gf != null)
				gf.visible = false;
		}

		stagesFunc((stage:BaseStage) -> stage.createPost());

		add(comboGroup = new FlxTypedGroup<Combo>());

		if(ClientPrefs.data.showNoteTiming)
		{
			add(noteTimingRating = new FlxText(0, 0, 0, "0ms"));
			noteTimingRating.setFormat(Paths.font("vcr.ttf"), 20, FlxColor.WHITE, CENTER, OUTLINE, FlxColor.BLACK);
			noteTimingRating.cameras = [camHUD];
			noteTimingRating.exists = false;
		}

		add(uiGroup = new FlxSpriteGroup());
		add(noteGroup = new FlxTypedGroup<FlxBasic>());

		Conductor.songPosition = -5000 / Conductor.songPosition;

		final showTime:Bool = ClientPrefs.data.timeBarType != 'Disabled';

		timeTxt = new FlxText(STRUM_X + (FlxG.width / 2) - 248, 19, 400, "", 32);
		timeTxt.setFormat(Paths.font("vcr.ttf"), 32, FlxColor.WHITE, CENTER, OUTLINE, FlxColor.BLACK);
		timeTxt.scrollFactor.set();
		timeTxt.alpha = 0;
		timeTxt.borderSize = 2;
		timeTxt.visible = updateTime = showTime;

		if (ClientPrefs.data.downScroll)
			timeTxt.y = FlxG.height - 44;

		if (ClientPrefs.data.timeBarType == 'Song Name')
			timeTxt.text = SONG.song;

		timeBar = new Bar(0, timeTxt.y + (timeTxt.height / 4), 'timeBar', () -> return songPercent, 0, 1);
		timeBar.scrollFactor.set();
		timeBar.screenCenter(X);
		timeBar.alpha = 0;
		timeBar.visible = showTime;

		uiGroup.add(timeBar);
		uiGroup.add(timeTxt);

		noteGroup.add(strumLineNotes = new FlxTypedGroup<StrumNote>());
		strumLineNotes.autoRefresh = strumLineNotes.useZIndex = false;

		if (ClientPrefs.data.timeBarType == 'Song Name')
		{
			timeTxt.size = 24;
			timeTxt.y += 3;
		}

		SustainSplash.init(grpHoldSplashes, Conductor.stepCrochet * 1.25 / 1000 / playbackRate, Math.floor((isPixelStage ? 20 : 24) / 100 * PlayState.SONG.bpm));

		opponentStrums = new FlxTypedGroup<StrumNote>();
		playerStrums = new FlxTypedGroup<StrumNote>();

		generateSong(SONG.song);

		noteGroup.add(grpNoteSplashes);
		noteGroup.add(grpHoldSplashes);

		add(camFollow = new FlxObject(0, 0, 1, 1));
		camFollow.setPosition(camPos.x, camPos.y);
		camPos.put();

		if (prevCamFollow != null)
		{
			camFollow = prevCamFollow;
			prevCamFollow = null;
		}

		FlxG.camera.follow(camFollow, LOCKON, 0);
		FlxG.camera.zoom = defaultCamZoom;
		FlxG.camera.snapToTarget();

		FlxG.worldBounds.set(0, 0, FlxG.width, FlxG.height);
		moveCameraSection();

		uiGroup.add(healthBar = new Bar(0, FlxG.height * (!ClientPrefs.data.downScroll ? 0.89 : 0.11), 'healthBar', () -> return health, 0, 2));
		healthBar.screenCenter(X);
		healthBar.leftToRight = characterPlayingAsDad;
		healthBar.flipX = false;
		healthBar.scrollFactor.set();
		healthBar.visible = !ClientPrefs.data.hideHud;
		healthBar.alpha = ClientPrefs.data.healthBarAlpha;
		reloadHealthBarColors();

		uiGroup.add(iconP1 = new HealthIcon(boyfriend.healthIcon, true));
		iconP1.y = healthBar.y - 75;
		iconP1.visible = !ClientPrefs.data.hideHud;
		iconP1.alpha = ClientPrefs.data.healthBarAlpha;

		uiGroup.add(iconP2 = new HealthIcon(dad.healthIcon, false));
		iconP2.y = healthBar.y - 75;
		iconP2.visible = !ClientPrefs.data.hideHud;
		iconP2.alpha = ClientPrefs.data.healthBarAlpha;

		uiGroup.add(scoreTxt = new FlxText(0, healthBar.y + 40, FlxG.width, "", 20));
		scoreTxt.setFormat(Paths.font("vcr.ttf"), 20, FlxColor.WHITE, CENTER, OUTLINE, FlxColor.BLACK);
		scoreTxt.scrollFactor.set();
		scoreTxt.borderSize = 1.25;
		scoreTxt.visible = !ClientPrefs.data.hideHud && !cpuControlled;
		updateScore();

		uiGroup.add(botplayTxt = new FlxText(400, healthBar.y - 90, FlxG.width - 800, "BOTPLAY", 32));
		botplayTxt.setFormat(Paths.font("vcr.ttf"), 32, FlxColor.WHITE, CENTER, OUTLINE, FlxColor.BLACK);
		botplayTxt.scrollFactor.set();
		botplayTxt.borderSize = 1.25;
		botplayTxt.visible = cpuControlled;

		if (ClientPrefs.data.downScroll)
			botplayTxt.y = healthBar.y + 70;

		noteGroup.cameras = [camHUD];
		uiGroup.cameras = [camHUD];
		comboGroup.cameras = [camHUD];

		startingSong = true;

		#if FEATURE_LUA
		for (notetype in noteTypes)
			startLuasNamed('custom_notetypes/$notetype');

		for (event in eventsPushed)
			startLuasNamed('custom_events/$event');
		#end

		#if FEATURE_HSCRIPT
		for (notetype in noteTypes)
			startHScriptsNamed('custom_notetypes/$notetype');

		for (event in eventsPushed)
			startHScriptsNamed('custom_events/$event');
		#end

		noteTypes = null;
		eventsPushed = null;

		if (eventNotes.length > 1)
		{
			for (event in eventNotes)
				event.strumTime -= eventEarlyTrigger(event);

			eventNotes.sort(sortByTime);
		}

		// SONG SPECIFIC SCRIPTS
		#if (FEATURE_LUA || FEATURE_HSCRIPT)
		for (folder in Mods.directoriesWithFile(Paths.getSharedPath(), 'data/$songName/'))
		{
			for (file in FileSystem.readDirectory(folder))
			{
				#if FEATURE_LUA
				startLuasNamed(folder + file);
				#end

				#if FEATURE_HSCRIPT
				startHScriptsNamed(folder + file);
				#end
			}
		}
		#end

		if (isPixelStage)
		{
			for (cam in FlxG.cameras.list)
				RetroCameraFade.fadeBlack(cam, 10, 1);
		}

		startCallback();

		FlxG.stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyPress);
		FlxG.stage.addEventListener(KeyboardEvent.KEY_UP, onKeyRelease);

		// PRECACHING THINGS THAT GET USED FREQUENTLY TO AVOID LAGSPIKES
		// bro maybe do this shit in loading state?
		if (ClientPrefs.data.hitsoundVolume > 0)
			Paths.sound('hitsound');

		for (i in 1...4)
			Paths.sound('missnote$i');

		Paths.image('alphabet');

		if (PauseSubState.songName != null)
			Paths.music(PauseSubState.songName);
		else if (Paths.formatToSongPath(ClientPrefs.data.pauseMusic) != 'none')
			Paths.music(Paths.formatToSongPath(ClientPrefs.data.pauseMusic));

		resetRPC();

		cacheCountdown();

		#if FEATURE_MOBILE_CONTROLS
		addMobileControls(false);
		mobileControls.instance.visible = true;
		mobileControls.onButtonDown.add(onButtonPress);
		mobileControls.onButtonUp.add(onButtonRelease);
		addTouchPad("NONE", "P");
		addTouchPadCamera(false);
		mobileControls.instance.forEachAlive((button) ->
		{
			if (touchPad.buttonP != null)
				button.deadZones.push(touchPad.buttonP);
		});
		#end

		super.create();
		Paths.clearUnusedMemory();

		if (eventNotes.length < 1)
			checkEventNote();
	}

	function set_songSpeed(value:Float):Float
	{
		if (generatedMusic)
		{
			final ratio:Float = value / songSpeed;
			if (ratio != 1)
			{
				for (note in notes.members)
					note.resizeByRatio(ratio);

				for (note in unspawnNotes)
					note.resizeByRatio(ratio);
			}
		}

		noteKillOffset = Math.max(Conductor.stepCrochet, 350 / value * playbackRate);
		return songSpeed = value;
	}

	function set_playbackRate(value:Float):Float
	{
		if (generatedMusic)
		{
			vocals.pitch = value;

			if (opponentVocals != null)
				opponentVocals.pitch = value;

			FlxG.sound.music.pitch = value;

			for (intro in [intro3Sound, intro2Sound, intro1Sound, introGoSound, missnoteSound])
			{
				if (intro != null)
					intro.pitch = value;
			}

			final ratio:Float = playbackRate / value;
			if (ratio != 1)
			{
				for (note in notes.members)
					note.resizeByRatio(ratio);

				for (note in unspawnNotes)
					note.resizeByRatio(ratio);
			}
		}

		FlxG.animationTimeScale = value;
		Conductor.safeZoneOffset = (ClientPrefs.data.safeFrames / 60) * 1000 * value;

		#if FEATURE_VIDEOS
		if (videoCutscene?.videoSprite?.bitmap != null)
			videoCutscene.videoSprite.bitmap.rate = value;
		#end

		setOnScripts('playbackRate', value);
		return playbackRate = value;
	}

	public function reloadHealthBarColors():Void
		healthBar.setColors(FlxColor.fromRGB(dad.healthColorArray[0], dad.healthColorArray[1], dad.healthColorArray[2]), FlxColor.fromRGB(boyfriend.healthColorArray[0], boyfriend.healthColorArray[1], boyfriend.healthColorArray[2]));

	public function addCharacterToList(newCharacter:String, type:Int):Void
	{
		switch (type)
		{
			case 0:
				if (!boyfriendMap.exists(newCharacter))
				{
					final newBoyfriend:Character = new Character(0, 0, newCharacter, !characterPlayingAsDad);
					if (characterPlayingAsDad) newBoyfriend.flipX = !newBoyfriend.flipX;
					boyfriendMap.set(newCharacter, newBoyfriend);
					boyfriendGroup.add(newBoyfriend);
					startCharacterPos(newBoyfriend);
					newBoyfriend.alpha = 0.00001;
					startCharacterScripts(newBoyfriend.curCharacter);
				}

			case 1:
				if (!dadMap.exists(newCharacter))
				{
					final newDad:Character = new Character(0, 0, newCharacter, characterPlayingAsDad);
					if (characterPlayingAsDad) newDad.flipX = !newDad.flipX;
					dadMap.set(newCharacter, newDad);
					dadGroup.add(newDad);
					startCharacterPos(newDad, true);
					newDad.alpha = 0.00001;
					startCharacterScripts(newDad.curCharacter);
				}

			case 2:
				if (gf != null && !gfMap.exists(newCharacter))
				{
					final newGf:Character = new Character(0, 0, newCharacter);
					newGf.scrollFactor.set(0.95, 0.95);
					gfMap.set(newCharacter, newGf);
					gfGroup.add(newGf);
					startCharacterPos(newGf);
					newGf.alpha = 0.00001;
					startCharacterScripts(newGf.curCharacter);
				}
		}
	}

	function startCharacterScripts(name:String):Void
	{
		#if FEATURE_LUA
		startLuasNamed('characters/$name');
		#end

		#if FEATURE_HSCRIPT
		startHScriptsNamed('characters/$name');
		#end
	}

	function startCharacterPos(char:Character, ?gfCheck:Bool = false):Void
	{
		if (gfCheck && char.curCharacter.startsWith('gf')) // IF DAD IS GIRLFRIEND, HE GOES TO HER POSITION
		{
			char.setPosition(GF_X, GF_Y);
			char.scrollFactor.set(0.95, 0.95);
			char.danceEveryNumBeats = 2;
		}
		char.x += char.positionArray[0];
		char.y += char.positionArray[1];
	}

	public var videoCutscene:VideoSprite = null;

	public function startVideo(name:String, forMidSong:Bool = false, canSkip:Bool = true, loop:Bool = false, playOnLoad:Bool = true):VideoSprite
	{
		#if FEATURE_VIDEOS
		inCutscene = !forMidSong;
		canPause = forMidSong;

		final fileName:String = Paths.video(name);

		if (FileSystem.exists(fileName))
		{
			videoCutscene = new VideoSprite(fileName, forMidSong, canSkip, loop);
			if (forMidSong) videoCutscene.videoSprite.bitmap.rate = playbackRate;

			// Finish callback
			if (!forMidSong)
			{
				function onVideoEnd():Void
				{
					if (!isDead && generatedMusic && PlayState.SONG.notes[Std.int(curStep / 16)] != null && !endingSong && !isCameraOnForcedPos)
					{
						moveCameraSection();
						FlxG.camera.snapToTarget();
					}
					videoCutscene = null;
					canPause = true;
					inCutscene = false;
					startAndEnd();
				}

				videoCutscene.finishCallback = onVideoEnd;
				videoCutscene.onSkip = onVideoEnd;
			}

			GameOverSubstate.instance != null && isDead ? GameOverSubstate.instance.add(videoCutscene) : add(videoCutscene);

			if (playOnLoad) videoCutscene.play();
			return videoCutscene;
		}
		#if (FEATURE_LUA || FEATURE_HSCRIPT)
		else
			addTextToDebug('Video not found: $fileName', FlxColor.RED);
		#else
		else
			FlxG.log.error('Video not found: $fileName');
		#end
		#else
		FlxG.log.warn('Platform not supported!');
		startAndEnd();
		#end
		return null;
	}

	inline function startAndEnd():Void
		endingSong ? endSong() : startCountdown();

	public function startDialogue(dialogueFile:DialogueFile, ?song:String = null):Void
	{
		// TO DO: Make this more flexible, maybe?
		if (psychDialogue != null)
			return;

		if (dialogueFile.dialogue.length > 0)
		{
			inCutscene = true;
			add(psychDialogue = new DialogueBoxPsych(dialogueFile, song));
			psychDialogue.scrollFactor.set();
			psychDialogue.finishThing = () ->
			{
				psychDialogue = null;
				startAndEnd();
			}
			psychDialogue.nextDialogueThing = startNextDialogue;
			psychDialogue.skipDialogueThing = skipDialogue;
			psychDialogue.cameras = [camHUD];
		}
		else
		{
			FlxG.log.warn('Your dialogue file is badly formatted!');
			startAndEnd();
		}
	}

	function cacheCountdown():Void
	{
		final introAss:Array<String> = getIntroAssets();

		for (image in introAss)
			Paths.image(image);

		for (sound in ['intro3', 'intro2', 'intro1', 'introGo'])
			Paths.sound(sound + introSoundsSuffix);
	}

	public function startCountdown():Bool
	{
		if (startedCountdown)
		{
			callOnScripts('onStartCountdown');
			return false;
		}

		seenCutscene = true;
		inCutscene = false;

		final ret:Dynamic = callOnScripts('onStartCountdown', null, true);
		if (ret != ScriptResult.Stop)
		{
			if (skipCountdown || startOnTime > 0)
				skipArrowStartTween = true;

			if (!characterPlayingAsDad)
			{
				generateStaticArrows(0, SONG.opponentArrowSkin);
				generateStaticArrows(1, SONG.playerArrowSkin);
			}
			else
			{
				generateStaticArrows(1, SONG.opponentArrowSkin);
				generateStaticArrows(0, SONG.playerArrowSkin);
			}

			if (characterPlayingAsDad && !ClientPrefs.data.middleScroll)
			{
				for (i in 0...opponentStrums.members.length)
				{
					final prevX:Float = opponentStrums.members[i].x;
					opponentStrums.members[i].x = playerStrums.members[i].x;
					playerStrums.members[i].x = prevX;
				}
			}

			for (i in 0...playerStrums.length)
			{
				setOnScripts('defaultPlayerStrumX' + i, playerStrums.members[i].x);
				setOnScripts('defaultPlayerStrumY' + i, playerStrums.members[i].y);
			}

			for (i in 0...opponentStrums.length)
			{
				setOnScripts('defaultOpponentStrumX' + i, opponentStrums.members[i].x);
				setOnScripts('defaultOpponentStrumY' + i, opponentStrums.members[i].y);
			}

			startedCountdown = true;
			Conductor.songPosition = -Conductor.crochet * 5;
			setOnScripts('startedCountdown', true);
			callOnScripts('onCountdownStarted', null);

			var swagCounter:Int = 0;
			if (startOnTime > 0)
			{
				clearNotesBefore(startOnTime);
				setSongTime(startOnTime - 350);
				return true;
			}
			else if (skipCountdown)
			{
				setSongTime(0);
				return true;
			}

			moveCameraSection();

			final introAss:Array<String> = getIntroAssets();

			startTimer = new FlxTimer().start(Conductor.crochet / 1000 / playbackRate, (tmr:FlxTimer) ->
			{
				characterBopper(tmr.loopsLeft);

				var tick:Countdown = THREE;

				switch (swagCounter)
				{
					case 0:
						intro3Sound = FlxG.sound.play(Paths.sound('intro3$introSoundsSuffix'), 0.6);
						if (intro3Sound != null) intro3Sound.pitch = playbackRate;
						tick = THREE;

					case 1:
						countdownReady = createCountdownSprite(introAss[0]);
						intro2Sound = FlxG.sound.play(Paths.sound('intro2$introSoundsSuffix'), 0.6);
						if (intro2Sound != null) intro2Sound.pitch = playbackRate;
						tick = TWO;

					case 2:
						countdownSet = createCountdownSprite(introAss[1]);
						intro1Sound = FlxG.sound.play(Paths.sound('intro1$introSoundsSuffix'), 0.6);
						if (intro1Sound != null) intro1Sound.pitch = playbackRate;
						tick = ONE;

					case 3:
						countdownGo = createCountdownSprite(introAss[2]);
						introGoSound = FlxG.sound.play(Paths.sound('introGo$introSoundsSuffix'), 0.6);
						if (introGoSound != null) introGoSound.pitch = playbackRate;
						tick = GO;

					case 4:
						tick = START;
				}

				notes.forEachAlive((note:Note) ->
				{
					if (ClientPrefs.data.opponentStrums || note.mustPress)
					{
						note.copyAlpha = false;
						note.alpha = note.multAlpha;

						if (ClientPrefs.data.middleScroll && !note.mustPress)
							note.alpha *= 0.35;
					}
				});

				stagesFunc((stage:BaseStage) -> stage.countdownTick(tick, swagCounter));
				callOnLuas('onCountdownTick', [swagCounter]);
				callOnHScript('onCountdownTick', [tick, swagCounter]);

				swagCounter += 1;
			}, 5);
		}
		return true;
	}

	inline function getIntroAssets():Array<String>
	{
		switch (stageUI)
		{
			case "pixel": return ['${stageUI}UI/ready-pixel', '${stageUI}UI/set-pixel', '${stageUI}UI/date-pixel'];
			case "normal": return ["ready", "set", "go"];
			default: return ['${stageUI}UI/ready', '${stageUI}UI/set', '${stageUI}UI/go'];
		};
	}

	function createCountdownSprite(image:String):FlxSprite
	{
		final spr:FlxSprite = new FlxSprite(0, 0, Paths.image(image));
		spr.cameras = [camHUD];
		spr.scrollFactor.set();
		spr.updateHitbox();

		if (PlayState.isPixelStage)
			spr.setGraphicSize(Std.int(spr.width * daPixelZoom));

		spr.screenCenter();
		spr.antialiasing = ClientPrefs.data.antialiasing && !isPixelStage;
		insert(members.indexOf(noteGroup), spr);
		FlxTween.tween(spr, {alpha: 0}, Conductor.crochet / 1000, {
			ease: isPixelStage ? EaseUtil.stepped(8) : FlxEase.cubeInOut,
			onComplete: (_) ->
			{
				remove(spr);
				spr.destroy();
			}
		});
		return spr;
	}

	public function addBehindGF(obj:FlxBasic):Void
		insert(members.indexOf(gfGroup), obj);

	public function addBehindBF(obj:FlxBasic):Void
		insert(members.indexOf(boyfriendGroup), obj);

	public function addBehindDad(obj:FlxBasic):Void
		insert(members.indexOf(dadGroup), obj);

	public function clearNotesBefore(time:Float)
	{
		var i:Int = unspawnNotes.length - 1;
		while (i >= 0)
		{
			final daNote:Note = unspawnNotes[i];
			if (daNote.strumTime - 350 < time)
			{
				daNote.active = daNote.visible = false;
				daNote.ignoreNote = true;

				if (!ClientPrefs.data.lowQuality || !ClientPrefs.data.popUpRating || !cpuControlled)
					daNote.kill();

				unspawnNotes.remove(daNote);
				daNote.recycle();
			}
			--i;
		}

		i = notes.length - 1;
		while (i >= 0)
		{
			final daNote:Note = notes.members[i];
			if (daNote.strumTime - 350 < time)
			{
				daNote.active = daNote.visible = false;
				daNote.ignoreNote = true;
				invalidateNote(daNote);
			}
			--i;
		}
	}

	// fun fact: Dynamic Functions can be overriden by just doing this
	// `updateScore = function(miss:Bool = false) { ... }
	// its like if it was a variable but its just a function!
	// cool right? -Crow
	// idrc fr fr -Sai
	public dynamic function updateScore():Void
	{
		final ret:Dynamic = callOnScripts('preUpdateScore', [], true);
		if (ret == ScriptResult.Stop) return;

		var str:String = ratingName;
		if (totalPlayed != 0)
		{
			final percent:Float = CoolUtil.floorDecimal(ratingPercent * 100, 2);
			str += ' (${percent}%) - ${ratingFC}';
		}

		final tempScore:String = 'Score: ${FlxStringUtil.formatMoney(songScore, false)}' + (!instakillOnMiss ? ' | Misses: ${songMisses}' : "") + ' | Rating: ${str}';
		// "tempScore" variable is used to prevent another memory leak, just in case
		// "\n" here prevents the text from being cut off by beat zooms
		// wha
		scoreTxt.text = '${tempScore}\n';

		callOnScripts('onUpdateScore', []);
	}

	public dynamic function fullComboFunction():Void
	{
		var sicks:Int = ratingsData[0].hits;
		var goods:Int = ratingsData[1].hits;
		var bads:Int = ratingsData[2].hits;
		var shits:Int = ratingsData[3].hits;

		ratingFC = "";
		if (songMisses == 0)
		{
			if (bads > 0 || shits > 0)
				ratingFC = 'FC';
			else if (goods > 0)
				ratingFC = 'GFC';
			else if (sicks > 0)
				ratingFC = 'SFC';
		}
		else ratingFC = songMisses < 10 ? 'SDCB' : 'Clear';
	}

	public function doScoreBop():Void
	{
		if (!ClientPrefs.data.scoreZoom) return;
		scoreTxtTween?.cancel();
		scoreTxt.scale.set(1.075, 1.075);
		scoreTxtTween = FlxTween.tween(scoreTxt.scale, {x: 1, y: 1}, 0.2);
	}

	public function setSongTime(time:Float):Void
	{
		if (time < 0)
			time = 0;

		FlxG.sound.music.pause();
		vocals.pause();
		opponentVocals?.pause();

		FlxG.sound.music.time = time;
		FlxG.sound.music.pitch = playbackRate;
		FlxG.sound.music.play();

		if (Conductor.songPosition <= vocals.length)
		{
			vocals.time = time;

			if (opponentVocals != null)
				opponentVocals.time = time;

			vocals.pitch = playbackRate;

			if (opponentVocals != null)
				opponentVocals.pitch = playbackRate;
		}

		vocals.play();
		opponentVocals?.play();

		Conductor.songPosition = time;
	}

	public function startNextDialogue():Void
	{
		dialogueCount++;
		callOnScripts('onNextDialogue', [dialogueCount]);
	}

	public function skipDialogue():Void
		callOnScripts('onSkipDialogue', [dialogueCount]);

	function startSong():Void
	{
		startingSong = false;

		FlxG.sound.playMusic(Paths.inst(SONG.song, Difficulty.getSongPrefix(null, false)), 1, false);
		FlxG.sound.music.pitch = playbackRate;
		FlxG.sound.music.onComplete = () -> finishSong();

		vocals.play();
		opponentVocals?.play();

		setSongTime(Math.max(0, startOnTime - 500));
		startOnTime = 0;

		if (paused)
		{
			FlxG.sound.music.pause();

			vocals.pause();
			opponentVocals?.pause();
		}

		// Song duration in a float, useful for the time left feature
		songLength = FlxG.sound.music.length;
		FlxTween.tween(timeBar, {alpha: 1}, 0.5, {ease: FlxEase.circOut});
		FlxTween.tween(timeTxt, {alpha: 1}, 0.5, {ease: FlxEase.circOut});

		#if FEATURE_DISCORD_RPC
		// Updating Discord Rich Presence (with Time Left)
		if (autoUpdateRPC)
			DiscordClient.changePresence(detailsText, '${SONG.song} ($storyDifficultyText)', iconP2.getCharacter(), true, songLength);
		#end

		setOnScripts('songLength', songLength);
		callOnScripts('onSongStart');

		#if (target.threaded)
		runSongSyncThread();
		#end
	}

	function generateSong(dataPath:String):Void
	{
		songSpeed = PlayState.SONG.speed;
		songSpeedType = ClientPrefs.getGameplaySetting('scrolltype');
		switch (songSpeedType)
		{
			case "multiplicative":
				songSpeed = SONG.speed * ClientPrefs.getGameplaySetting('scrollspeed');
			case "constant":
				songSpeed = ClientPrefs.getGameplaySetting('scrollspeed');
		}

		Conductor.bpm = SONG.bpm;

		vocals = new FlxSound();
		opponentVocals = new FlxSound();
		try
		{
			if (SONG.needsVoices)
			{
				final playerVocals = Paths.voices(SONG.song, boyfriend.vocalsFile + Difficulty.getSongPrefix()) ?? Paths.voices(SONG.song, 'Player' + Difficulty.getSongPrefix());
				vocals.loadEmbedded(playerVocals ?? Paths.voices(SONG.song, Difficulty.getSongPrefix(null, false)));
				vocals.pitch = playbackRate;

				final oppVocals = Paths.voices(SONG.song, dad.vocalsFile + Difficulty.getSongPrefix()) ?? Paths.voices(SONG.song, 'Opponent' + Difficulty.getSongPrefix());
				if (oppVocals != null) {
					opponentVocals.loadEmbedded(oppVocals);
					opponentVocals.pitch = playbackRate;
				}
			}
		}
		catch (e:Dynamic) {}

		noteGroup.add(notes = new FlxTypedGroup<Note>());

		isErect = Difficulty.list[storyDifficulty] == ERECT || Difficulty.list[storyDifficulty] == NIGHTMARE;

		final file:String = 'events${isErect ? '-erect' : ''}';
		if (#if FEATURE_MODS FileSystem.exists(Paths.modsJson('$songName/$file')) || #end FileSystem.exists(Paths.json('$songName/$file')))
		{
			final eventsData:Array<Dynamic> = Song.loadFromJson(file, songName).events;
			for (event in eventsData)
			{
				for (i in 0...event[1].length)
					makeEvent(event, i);
			}
		}

		final isPsychRelease:Bool = SONG.format == 'psych_v1';

		for (section in SONG.notes)
		{
			for (songNotes in section.sectionNotes)
			{
				var daStrumTime:Float = songNotes[0];
				var daNoteData:Int = Std.int(songNotes[1] % 4);
				var gottaHitNote:Bool = section.mustHitSection;

				if (!isPsychRelease)
				{
					if (songNotes[1] > 3)
					{
						gottaHitNote = !section.mustHitSection;
					}
				}
				else
				{
					gottaHitNote = songNotes[1] < 4;
				}

				if (characterPlayingAsDad)
					gottaHitNote = !gottaHitNote;

				var oldNote:Note = unspawnNotes.length > 0  ? unspawnNotes[Std.int(unspawnNotes.length - 1)] : null;

				var swagNote:Note = Note.getNote(daStrumTime, daNoteData, oldNote);
				swagNote.mustPress = gottaHitNote;
				if (!gottaHitNote && allowedNotes.contains(swagNote.noteType))
					swagNote.texture = SONG.opponentArrowSkin;
				swagNote.sustainLength = songNotes[2];
				swagNote.gfNote = (section.gfSection && (songNotes[1] < 4));
				swagNote.noteType = songNotes[3];
				if (!Std.isOfType(songNotes[3], String))
					swagNote.noteType = ChartingState.noteTypeList[songNotes[3]]; // Backward compatibility + compatibility with Week 7 charts

				swagNote.scrollFactor.set();

				unspawnNotes.push(swagNote);

				final susLength:Float = swagNote.sustainLength / Conductor.stepCrochet;
				final floorSus:Int = Math.floor(susLength);

				if (floorSus > 0)
				{
					for (susNote in 0...floorSus + 1)
					{
						oldNote = unspawnNotes[Std.int(unspawnNotes.length - 1)];

						var sustainNote:Note = Note.getNote(daStrumTime + (Conductor.stepCrochet * susNote), daNoteData, oldNote, true);
						sustainNote.mustPress = gottaHitNote;
						sustainNote.gfNote = (section.gfSection && (songNotes[1] < 4));
						sustainNote.noteType = swagNote.noteType;
						sustainNote.scrollFactor.set();
						sustainNote.parent = swagNote;
						if (!gottaHitNote && allowedNotes.contains(sustainNote.noteType))
							sustainNote.texture = SONG.opponentArrowSkin;
						unspawnNotes.push(sustainNote);
						swagNote.tail.push(sustainNote);

						sustainNote.correctionOffset = swagNote.height / 2;
						if (!PlayState.isPixelStage)
						{
							if (oldNote.isSustainNote)
							{
								oldNote.scale.y *= Note.SUSTAIN_SIZE / oldNote.frameHeight;
								oldNote.scale.y /= playbackRate;
								oldNote.updateHitbox();
							}

							if (ClientPrefs.data.downScroll)
								sustainNote.correctionOffset = 0;
						}
						else if (oldNote.isSustainNote)
						{
							oldNote.scale.y /= playbackRate;
							oldNote.updateHitbox();
						}

						if (sustainNote.mustPress)
							sustainNote.x += FlxG.width / 2; // general offset
						else if (ClientPrefs.data.middleScroll)
						{
							sustainNote.x += 310;
							if (daNoteData > 1) // Up and Right
								sustainNote.x += FlxG.width / 2 + 25;
						}
					}
				}

				if (swagNote.mustPress)
				{
					swagNote.x += FlxG.width / 2; // general offset
				}
				else if (ClientPrefs.data.middleScroll)
				{
					swagNote.x += 310;
					if (daNoteData > 1) // Up and Right
					{
						swagNote.x += FlxG.width / 2 + 25;
					}
				}

				if (!noteTypes.contains(swagNote.noteType))
				{
					noteTypes.push(swagNote.noteType);
				}
			}
		}
		for (event in SONG.events) // Event Notes
			for (i in 0...event[1].length)
				makeEvent(event, i);

		unspawnNotes.sort(sortByTime);
		generatedMusic = true;
	}

	// called only once per different event (Used for precaching)
	function eventPushed(event:EventNote)
	{
		eventPushedUnique(event);
		if (eventsPushed.contains(event.event)) return;
		stagesFunc(function(stage:BaseStage) stage.eventPushed(event));
		eventsPushed.push(event.event);
	}

	// called by every event with the same name
	function eventPushedUnique(event:EventNote)
	{
		switch (event.event)
		{
			case "Change Character":
				var charType:Int = 0;
				switch (event.value1.toLowerCase())
				{
					case 'gf' | 'girlfriend' | '1':
						charType = 2;
					case 'dad' | 'opponent' | '0':
						charType = 1;
					default:
						var val1:Int = Std.parseInt(event.value1);
						if (Math.isNaN(val1))
							val1 = 0;
						charType = val1;
				}

				var newCharacter:String = event.value2;
				addCharacterToList(newCharacter, charType);

			case 'Play Sound':
				Paths.sound(event.value1); // Precache sound
		}
		stagesFunc(function(stage:BaseStage) stage.eventPushedUnique(event));
	}

	function eventEarlyTrigger(event:EventNote):Float
	{
		final args:Array<Dynamic> = [event.event, event.value1, event.value2, event.strumTime];
		var returnedValue:Null<Float> = callOnScripts('eventEarlyTrigger', args, true, [], [0]);
		if (returnedValue != null && returnedValue != 0 /*&& returnedValue != ScriptResult.Continue*/)
		{
			return returnedValue;
		}
		return 0;
	}

	public static function sortByTime(Obj1:Dynamic, Obj2:Dynamic):Int
		return FlxSort.byValues(FlxSort.ASCENDING, Obj1.strumTime, Obj2.strumTime);

	function makeEvent(event:Array<Dynamic>, i:Int)
	{
		var subEvent:EventNote = {
			strumTime: event[0] + ClientPrefs.data.noteOffset,
			event: event[1][i][0],
			value1: event[1][i][1],
			value2: event[1][i][2]
		};
		eventNotes.push(subEvent);
		eventPushed(subEvent);
		final args:Array<Dynamic> = [
			subEvent.event,
			subEvent.value1 != null ? subEvent.value1 : '',
			subEvent.value2 != null ? subEvent.value2 : '',
			subEvent.strumTime
		];
		callOnScripts('onEventPushed', args);
	}

	public var skipArrowStartTween:Bool = false; // for lua

	private function generateStaticArrows(player:Int, skin:String):Void
	{
		var strumLineX:Float = ClientPrefs.data.middleScroll ? STRUM_X_MIDDLESCROLL : STRUM_X;
		var strumLineY:Float = ClientPrefs.data.downScroll ? (FlxG.height - 150) : 50;
		for (i in 0...4)
		{
			// FlxG.log.add(i);
			var targetAlpha:Float = 1;
			if (player < 1)
			{
				if (!ClientPrefs.data.opponentStrums)
					targetAlpha = 0;
				else if (ClientPrefs.data.middleScroll)
					targetAlpha = 0.35;
			}

			var babyArrow:StrumNote = new StrumNote(strumLineX, strumLineY, i, player, skin);
			babyArrow.downScroll = ClientPrefs.data.downScroll;
			if (!isStoryMode && !skipArrowStartTween)
			{
				// babyArrow.y -= 10;
				babyArrow.alpha = 0;
				FlxTween.tween(babyArrow, {/*y: babyArrow.y + 10,*/ alpha: targetAlpha}, 1, {ease: FlxEase.circOut, startDelay: 0.5 + (0.2 * i)});
			}
			else
				babyArrow.alpha = targetAlpha;

			if (player == 1)
				playerStrums.add(babyArrow);
			else
			{
				if (ClientPrefs.data.middleScroll)
				{
					babyArrow.x += 310;
					if (i > 1) // Up and Right
					{
						babyArrow.x += FlxG.width / 2 + 25;
					}
				}
				opponentStrums.add(babyArrow);
			}

			strumLineNotes.add(babyArrow);
			babyArrow.postAddedToGroup();
		}
	}

	@:haxe.warning("-WDeprecated")
	override function openSubState(SubState:FlxSubState):Void
	{
		stagesFunc((stage:BaseStage) -> stage.openSubState(SubState));

		if (paused)
		{
			if (FlxG.sound.music != null)
			{
				FlxG.sound.music.pause();
				vocals.pause();
				opponentVocals?.pause();
			}

			FlxTimer.globalManager.forEach((tmr:FlxTimer) -> if (!tmr.finished) tmr.active = false);
			FlxTween.globalManager.forEach((twn:FlxTween) -> if (!twn.finished) twn.active = false);

			#if FEATURE_MOBILE_CONTROLS
			mobileControls.instance.visible = touchPad.visible = false;
			#end
		}

		super.openSubState(SubState);
	}

	override function closeSubState():Void
	{
		stagesFunc((stage:BaseStage) -> stage.closeSubState());

		if (paused && !closedFromPause)
		{
			if (FlxG.sound.music != null && !startingSong)
				resyncVocals();

			FlxTimer.globalManager.forEach((tmr:FlxTimer) -> if (!tmr.finished) tmr.active = true);
			FlxTween.globalManager.forEach((twn:FlxTween) -> if (!twn.finished) twn.active = true);

			paused = closedFromPause =  false;

			#if FEATURE_MOBILE_CONTROLS
			mobileControls.instance.visible = touchPad.visible = true;
			#end

			resetRPC(startTimer != null && startTimer.finished);

			#if (target.threaded)
			runSongSyncThread();
			#end
		}
		super.closeSubState();
	}

	override public function onFocus():Void
	{
		if (health > 0 && !paused)
			resetRPC(Conductor.songPosition > 0.0);

		#if (target.threaded)
		shutdownThread = false;
		runSongSyncThread();
		#end

		super.onFocus();
	}

	override public function onFocusLost():Void
	{
		#if FEATURE_DISCORD_RPC
		if (health > 0 && !paused && FlxG.autoPause && autoUpdateRPC)
			DiscordClient.changePresence(detailsPausedText, '${SONG.song} ($storyDifficultyText)', iconP2.getCharacter());
		#end

		#if (target.threaded)
		shutdownThread = true;
		#end
		super.onFocusLost();
	}

	function resetRPC(?showTime:Bool = false)
	{
		#if FEATURE_DISCORD_RPC
		if (autoUpdateRPC)
			DiscordClient.changePresence(detailsText, '${SONG.song} ($storyDifficultyText)', iconP2.getCharacter(), showTime, showTime ? songLength - Conductor.songPosition - ClientPrefs.data.noteOffset : null);
		#end
	}

	function resyncVocals():Void
	{
		if (finishTimer != null)
			return;

		FlxG.sound.music.play();
		FlxG.sound.music.pitch = playbackRate;
		Conductor.songPosition = FlxG.sound.music.time + Conductor.offset;

		if (FlxG.sound.music.time < vocals.length)
		{
			vocals.time = FlxG.sound.music.time;
			vocals.pitch = playbackRate;
			vocals.play();
		} else vocals.pause();

		if (opponentVocals != null)
		{
			if (FlxG.sound.music.time < opponentVocals.length)
			{
				opponentVocals.time = FlxG.sound.music.time;
				opponentVocals.pitch = playbackRate;
				opponentVocals.play();
			} else opponentVocals.pause();
		}
	}

	public var paused:Bool = false;
	public var closedFromPause:Bool = false;
	public var canReset:Bool = true;

	var startedCountdown:Bool = false;
	var canPause:Bool = true;
	var freezeCamera:Bool = false;
	var allowDebugKeys:Bool = true;

	override public function update(elapsed:Float):Void
	{
		FlxG.camera.followLerp = !inCutscene && !paused && !freezeCamera ? 2.4 * cameraSpeed * playbackRate : 0;

		super.update(elapsed);

		if (!inCutscene && !paused && !freezeCamera && camTween != null)
			FlxG.camera.focusOn(FlxPoint.weak(camFollow.x, camFollow.y));

		if (botplayTxt != null && botplayTxt.visible)
		{
			botplaySine += 180 * elapsed;
			botplayTxt.alpha = 1 - Math.sin((Math.PI * botplaySine) / 180);
		}

		if ((Funkin.controls.PAUSE #if android || FlxG.android.justReleased.BACK #end #if FEATURE_MOBILE_CONTROLS || touchPad.buttonP.justPressed #end) && (startedCountdown && canPause))
		{
			var ret:Dynamic = callOnScripts('onPause', null, true);
			if (ret != ScriptResult.Stop)
				openPauseMenu();
		}

		if (!endingSong && !inCutscene && allowDebugKeys)
		{
			if (Funkin.controls.justPressed('debug_1'))
				openChartEditor();
			else if (Funkin.controls.justPressed('debug_2'))
				openCharacterEditor();
		}

		if (healthBar.bounds.max != null && health > healthBar.bounds.max)
			health = healthBar.bounds.max;

		updateIconsScale(elapsed);
		updateIconsPosition();
		updateIconsStatus();

		if (startedCountdown && !paused)
		{
			Conductor.songPosition += elapsed * 1000 * playbackRate;
			if (Conductor.songPosition >= 0)
			{
				var timeDiff:Float = Math.abs(FlxG.sound.music.time - Conductor.songPosition - Conductor.offset);
				Conductor.songPosition = FlxMath.lerp(Conductor.songPosition, FlxG.sound.music.time, FlxMath.bound(elapsed * 2.5, 0, 1));
				if (timeDiff > 1000 * playbackRate)
					Conductor.songPosition = Conductor.songPosition + 1000 * FlxMath.signOf(timeDiff);
			}
		}

		if (startingSong)
		{
			if (startedCountdown && Conductor.songPosition >= 0)
				startSong();
			else if (!startedCountdown)
				Conductor.songPosition = -Conductor.crochet * 5;
		}
		else if (!paused)
		{
			if (updateTime) //rating wasnt updated if false
			{
				var curTime:Float = Math.max(0, Conductor.songPosition - ClientPrefs.data.noteOffset);
				songPercent = (curTime / songLength);

				var songCalc:Float = (songLength - curTime);
				if (ClientPrefs.data.timeBarType == 'Time Elapsed')
					songCalc = curTime;

				var secondsTotal:Int = Math.floor(songCalc / 1000);
				if (secondsTotal < 0)
					secondsTotal = 0;

				if (ClientPrefs.data.timeBarType != 'Song Name' && secondsTotal != _lastSecondsTotal)
				{
					_lastSecondsTotal = secondsTotal;
					timeTxt.text = FlxStringUtil.formatTime(secondsTotal, false);
				}
			}

			if (songScore != _lastSongScore || songMisses != _lastSongMisses || songHits != _lastSongHits || combo != _lastCombo || totalPlayed != _lastTotalPlayed || totalNotesHit != _lastTotalNotesHit)
			{
				_lastSongScore = songScore;
				_lastSongMisses = songMisses;
				_lastSongHits = songHits;
				_lastCombo = combo;
				_lastTotalPlayed = totalPlayed;
				_lastTotalNotesHit = totalNotesHit;
				recalculateRating();
			}
		}

		if (!inCutscene && !paused && !freezeCamera)
		{
			FlxG.camera.zoom = zoomTween != null ? defaultCamZoom : FlxMath.lerp(defaultCamZoom, FlxG.camera.zoom, Math.exp(-elapsed * 3.125 * camZoomingDecay * playbackRate));
			camHUD.zoom = FlxMath.lerp(1, camHUD.zoom, Math.exp(-elapsed * 3.125 * camZoomingDecay * playbackRate));
		}

		if (!ClientPrefs.data.noReset && Funkin.controls.RESET && canReset && !inCutscene && startedCountdown && !endingSong)
			health = 0;

		doDeathCheck();

		if (unspawnNotes[0] != null)
		{
			var time:Float = spawnTime * playbackRate;

			if (songSpeed < 1)
				time /= songSpeed;

			if (unspawnNotes[0].multSpeed < 1)
				time /= unspawnNotes[0].multSpeed;

			while (unspawnNotes.length > 0 && unspawnNotes[0].strumTime - Conductor.songPosition < time)
			{
				final dunceNote:Note = unspawnNotes.shift();
				notes.insert(0, dunceNote);
				dunceNote.spawned = true;

				if (dunceNote.mustPress && allowedNotes.contains(dunceNote.noteType))
					dunceNote.texture = noteSkin;
				else if (!dunceNote.mustPress && allowedNotes.contains(dunceNote.noteType))
					dunceNote.texture = noteSkin1;

				if (hasScripts)
				{
					final args:Array<Dynamic> = [0, dunceNote.noteData, dunceNote.noteType, dunceNote.isSustainNote, dunceNote.strumTime];
					callOnLuas('onSpawnNote', args);
					callOnHScript('onSpawnNote', [dunceNote]);
				}
			}
		}

		if (generatedMusic)
		{
			if (!inCutscene)
			{
				if (!cpuControlled)
					keysCheck();
				else
					playerDance();

				if (notes.length > 0)
				{
					if (startedCountdown)
					{
						final fakeCrochet:Float = (60 / SONG.bpm) * 1000;
						final scrollSpeed:Float = songSpeed / playbackRate;
						final members:Array<Note> = notes.members;
						var i:Int = members.length - 1;
						while (i >= 0)
						{
							if (i >= members.length)
							{
								i = members.length - 1;
								continue;
							}

							final daNote:Note = members[i];
							i--;
							if (daNote == null || !daNote.exists || !daNote.alive)
								continue;

							final strumGroup:FlxTypedGroup<StrumNote> = daNote.mustPress ? playerStrums : opponentStrums;
							final strum:StrumNote = strumGroup.members[daNote.noteData];
							daNote.followStrumNote(strum, fakeCrochet, scrollSpeed);

							if (daNote.mustPress)
							{
								if (cpuControlled
									&& !daNote.blockHit
									&& daNote.canBeHit
									&& (daNote.isSustainNote || daNote.strumTime <= Conductor.songPosition))
								{
									updateAna(daNote);
									goodNoteHit(daNote);
								}
							}
							else if (daNote.wasGoodHit && !daNote.hitByOpponent && !daNote.ignoreNote)
								opponentNoteHit(daNote);

							if (daNote.isSustainNote && strum.sustainReduce)
								daNote.clipToStrumNote(strum);

							// Kill extremely late notes and cause misses
							if (Conductor.songPosition - daNote.strumTime > noteKillOffset)
							{
								if (daNote.mustPress && !cpuControlled && !daNote.ignoreNote && !endingSong && (daNote.tooLate || !daNote.wasGoodHit))
									noteMiss(daNote);

								daNote.active = daNote.visible = false;
								invalidateNote(daNote);
							}
						}
					}
					else
					{
						for (daNote in notes.members)
						{
							if (daNote == null || !daNote.exists || !daNote.alive)
								continue;
							daNote.canBeHit = false;
							daNote.wasGoodHit = false;
						}
					}
				}
			}
			checkEventNote();
		}

		#if debug
		if (!endingSong && !startingSong)
		{
			if (FlxG.keys.justPressed.ONE)
			{
				KillNotes();
				FlxG.sound.music.onComplete();
			}
			if (FlxG.keys.justPressed.TWO) // Go 10 seconds into the future!! :OOO
			{
				setSongTime(Conductor.songPosition + 10000);
				clearNotesBefore(Conductor.songPosition);
			}
		}
		#end

		if(!ClientPrefs.data.hideHud)
		{
			for (s in comboGroup.members)
			{
				s.lifeTime -= elapsed * playbackRate;

				if (s.lifeTime <= 0)
				{
					s.alpha -= elapsed * 6 * playbackRate;

					if (s.alpha <= 0)
						s.kill();
				}
			}
		}

		if (hasScripts)
		{
			setOnScripts('cameraX', camFollow.x);
			setOnScripts('cameraY', camFollow.y);
			// Unlike the camera position, this only ever changes when botplay is toggled.
			if (cpuControlled != _lastCpuControlled)
			{
				_lastCpuControlled = cpuControlled;
				setOnScripts('botPlay', cpuControlled);
			}
		}
	}

	// Health icon updaters
	public dynamic function updateIconsScale(elapsed:Float)
	{
		var mult:Float = FlxMath.lerp(1, iconP1.scale.x, Math.exp(-elapsed * 9 * playbackRate));
		iconP1.scale.set(mult, mult);
		iconP1.updateHitbox();

		var mult:Float = FlxMath.lerp(1, iconP2.scale.x, Math.exp(-elapsed * 9 * playbackRate));
		iconP2.scale.set(mult, mult);
		iconP2.updateHitbox();
	}

	public dynamic function updateIconsPosition()
	{
		var iconOffset:Int = 26;
		iconP1.x = healthBar.barCenter + (150 * iconP1.scale.x - 150) / 2 - iconOffset;
		iconP2.x = healthBar.barCenter - (150 * iconP2.scale.x) / 2 - iconOffset * 2;
	}

	public dynamic function updateIconsStatus()
	{
		final iconP1HasLoseIcon:Bool = (iconP1.animation.curAnim.frames.length >= 2);
		final iconP1HasWinIcon:Bool = (iconP1.animation.curAnim.frames.length >= 3);
		final iconP2HasLoseIcon:Bool = (iconP2.animation.curAnim.frames.length >= 2);
		final iconP2HasWinIcon:Bool = (iconP2.animation.curAnim.frames.length >= 3);

		inline function setFrameIfDifferent(icon:HealthIcon, frame:Int)
		{
			if (icon.animation.curAnim.curFrame != frame)
				icon.animation.curAnim.curFrame = frame;
		}

		if (characterPlayingAsDad)
		{
			if (healthBar.percent > 80)
			{
				setFrameIfDifferent(iconP1, iconP1HasLoseIcon ? 1 : 0);
				setFrameIfDifferent(iconP2, iconP2HasWinIcon ? 2 : 0);
			}
			else if (healthBar.percent < 20)
			{
				setFrameIfDifferent(iconP2, iconP2HasLoseIcon ? 1 : 0);
				setFrameIfDifferent(iconP1, iconP1HasWinIcon ? 2 : 0);
			}
			else
			{
				setFrameIfDifferent(iconP1, 0);
				setFrameIfDifferent(iconP2, 0);
			}
		}
		else
		{
			if (healthBar.percent < 20)
			{
				setFrameIfDifferent(iconP1, iconP1HasLoseIcon ? 1 : 0);
				setFrameIfDifferent(iconP2, iconP2HasWinIcon ? 2 : 0);
			}
			else if (healthBar.percent > 80)
			{
				setFrameIfDifferent(iconP2, iconP2HasLoseIcon ? 1 : 0);
				setFrameIfDifferent(iconP1, iconP1HasWinIcon ? 2 : 0);
			}
			else
			{
				setFrameIfDifferent(iconP2, 0);
				setFrameIfDifferent(iconP1, 0);
			}
		}
	}

	function set_health(value:Float):Float
	{
		value = FlxMath.roundDecimal(value, 5); // Fix Float imprecision

		if (healthBar == null || !healthBar.enabled || healthBar.valueFunction == null)
		{
			health = value;
			return health;
		}

		// update health bar
		health = value;
		var newPercent:Null<Float> = FlxMath.remapToRange(FlxMath.bound(healthBar.valueFunction(), healthBar.bounds.min, healthBar.bounds.max),
			healthBar.bounds.min, healthBar.bounds.max, 0, 100);
		healthBar.percent = (newPercent != null ? newPercent : 0);
		return health;
	}

	function openPauseMenu()
	{
		FlxG.camera.followLerp = 0;
		persistentUpdate = false;
		persistentDraw = true;
		paused = true;

		if (FlxG.sound.music != null)
		{
			FlxG.sound.music.pause();
			vocals.pause();
			opponentVocals?.pause();
		}

		if (!cpuControlled)
		{
			for (note in playerStrums)
				if (note.animation.curAnim != null && note.animation.curAnim.name != 'static')
				{
					note.playAnim('static');
					note.resetAnim = 0;
				}
		}
		switchSubState(PauseSubState);

		#if FEATURE_DISCORD_RPC
		if (autoUpdateRPC)
			DiscordClient.changePresence(detailsPausedText, SONG.song + " (" + storyDifficultyText + ")", iconP2.getCharacter());
		#end
	}

	public function openChartEditor()
	{
		FlxG.camera.followLerp = 0;
		persistentUpdate = false;
		paused = true;
		FlxG.sound.music?.stop();
		chartingMode = true;

		#if FEATURE_DISCORD_RPC
		DiscordClient.changePresence("Chart Editor", null, null, true);
		DiscordClient.resetClientID();
		#end

		Funkin.switchState(ChartingState);
	}

	public function openCharacterEditor()
	{
		FlxG.camera.followLerp = 0;
		persistentUpdate = false;
		paused = true;
		FlxG.sound.music?.stop();
		#if FEATURE_DISCORD_RPC DiscordClient.resetClientID(); #end
		Funkin.switchState(CharacterEditorState, [SONG.player2]);
	}

	public var isDead:Bool = false; // Don't mess with this on Lua!!!

	function doDeathCheck(?skipHealthCheck:Bool = false)
	{
		if (((skipHealthCheck && instakillOnMiss) || health <= 0) && !practiceMode && !isDead)
		{
			var ret:Dynamic = callOnScripts('onGameOver', null, true);
			if (ret != ScriptResult.Stop)
			{
				FlxG.animationTimeScale = 1;
				boyfriend.stunned = true;
				deathCounter++;

				paused = true;
				canPause = false;

				#if FEATURE_VIDEOS
				videoCutscene = FlxDestroyUtil.destroy(videoCutscene);
				#end

				vocals.stop();
				opponentVocals?.stop();
				FlxG.sound.music.stop();

				persistentUpdate = false;
				persistentDraw = false;
				FlxTimer.globalManager.clear();
				FlxTween.globalManager.clear();

				#if FEATURE_LUA
				modchartTimers.clear();
				modchartTweens.clear();
				#end

				switchSubState(GameOverSubstate);

				#if FEATURE_DISCORD_RPC
				if (autoUpdateRPC)
					DiscordClient.changePresence("Game Over - " + detailsText, SONG.song + " (" + storyDifficultyText + ")", iconP2.getCharacter());
				#end

				isDead = true;
				return true;
			}
		}
		return false;
	}

	public function checkEventNote()
	{
		while (eventNotes.length > 0)
		{
			var leStrumTime:Float = eventNotes[0].strumTime;
			if (Conductor.songPosition < leStrumTime)
			{
				return;
			}

			var value1:String = '';
			if (eventNotes[0].value1 != null)
				value1 = eventNotes[0].value1;

			var value2:String = '';
			if (eventNotes[0].value2 != null)
				value2 = eventNotes[0].value2;

			triggerEvent(eventNotes[0].event, value1, value2, leStrumTime);
			eventNotes.shift();
		}
	}

	public function triggerEvent(eventName:String, value1:String, value2:String, strumTime:Float)
	{
		var flValue1:Null<Float> = Std.parseFloat(value1);
		var flValue2:Null<Float> = Std.parseFloat(value2);
		if (Math.isNaN(flValue1))
			flValue1 = null;
		if (Math.isNaN(flValue2))
			flValue2 = null;

		switch (eventName)
		{
			case 'Hey!':
				var value:Int = 2;
				switch (value1.toLowerCase().trim())
				{
					case 'bf' | 'boyfriend' | '0':
						value = 0;
					case 'gf' | 'girlfriend' | '1':
						value = 1;
				}

				if (flValue2 == null || flValue2 <= 0)
					flValue2 = 0.6;

				if (value != 0)
				{
					if (dad.curCharacter.startsWith('gf')) // Tutorial GF is actually Dad, what on my life I'm doing..-
					{
						dad.playAnim('cheer', true);
						dad.specialAnim = true;
						dad.heyTimer = flValue2;
					}
					else if (gf != null)
					{
						gf.playAnim('cheer', true);
						gf.specialAnim = true;
						gf.heyTimer = flValue2;
					}
				}
				if (value != 1)
				{
					boyfriend.playAnim('hey', true);
					boyfriend.specialAnim = true;
					boyfriend.heyTimer = flValue2;
				}

			case 'Set GF Speed':
				if (flValue1 == null || flValue1 < 1)
					flValue1 = 1;
				gfSpeed = Math.round(flValue1);

			case 'Add Camera Zoom':
				if (ClientPrefs.data.camZooms && FlxG.camera.zoom < 1.35)
				{
					if (flValue1 == null)
						flValue1 = 0.015;
					if (flValue2 == null)
						flValue2 = 0.03;

					FlxG.camera.zoom += flValue1;
					camHUD.zoom += flValue2;
				}

			case 'Play Animation':
				// trace('Anim to play: ' + value1);
				var char:Character = dad;
				switch (value2.toLowerCase().trim())
				{
					case 'bf' | 'boyfriend':
						char = boyfriend;
					case 'gf' | 'girlfriend':
						char = gf;
					default:
						if (flValue2 == null)
							flValue2 = 0;
						switch (Math.round(flValue2))
						{
							case 1: char = boyfriend;
							case 2: char = gf;
						}
				}

				if (char != null)
				{
					char.playAnim(value1, true);
					char.specialAnim = true;
				}

			case 'Camera Follow Pos':
				if (camFollow != null)
				{
					isCameraOnForcedPos = false;
					if (flValue1 != null || flValue2 != null)
					{
						isCameraOnForcedPos = true;
						if (flValue1 == null)
							flValue1 = 0;
						if (flValue2 == null)
							flValue2 = 0;

						camTween?.cancel();
						camTween = null;

						camFollow.x = flValue1;
						camFollow.y = flValue2;
					}
				}

			case 'Alt Idle Animation':
				var char:Character = dad;
				switch (value1.toLowerCase().trim())
				{
					case 'gf' | 'girlfriend':
						char = gf;
					case 'boyfriend' | 'bf':
						char = boyfriend;
					default:
						var val:Int = Std.parseInt(value1);
						if (Math.isNaN(val))
							val = 0;

						switch (val)
						{
							case 1: char = boyfriend;
							case 2: char = gf;
						}
				}

				if (char != null)
				{
					char.idleSuffix = value2;
					char.recalculateDanceIdle();
				}

			case 'Screen Shake':
				var valuesArray:Array<String> = [value1, value2];
				var targetsArray:Array<FlxCamera> = [camGame, camHUD];
				for (i in 0...targetsArray.length)
				{
					var split:Array<String> = valuesArray[i].split(',');
					var duration:Float = 0;
					var intensity:Float = 0;
					if (split[0] != null)
						duration = Std.parseFloat(split[0].trim());
					if (split[1] != null)
						intensity = Std.parseFloat(split[1].trim());
					if (Math.isNaN(duration))
						duration = 0;
					if (Math.isNaN(intensity))
						intensity = 0;

					if (duration > 0 && intensity != 0)
					{
						targetsArray[i].shake(intensity, duration);
					}
				}

			case 'Change Character':
				var charType:Int = 0;

				switch (value1.toLowerCase().trim())
				{
					case 'gf' | 'girlfriend':
						charType = 2;
					case 'dad' | 'opponent':
						charType = 1;
					default:
						charType = Std.parseInt(value1);
						if (Math.isNaN(charType)) charType = 0;
				}

				switch (charType)
				{
					case 0:
						if (boyfriend.curCharacter != value2)
						{
							if (!boyfriendMap.exists(value2))
							{
								addCharacterToList(value2, charType);
							}

							var lastAlpha:Float = boyfriend.alpha;
							boyfriend.alpha = 0.00001;
							boyfriend = boyfriendMap.get(value2);
							boyfriend.alpha = lastAlpha;
							iconP1.changeIcon(boyfriend.healthIcon);
						}
						setOnScripts('boyfriendName', boyfriend.curCharacter);

					case 1:
						if (dad.curCharacter != value2)
						{
							if (!dadMap.exists(value2))
							{
								addCharacterToList(value2, charType);
							}

							var wasGf:Bool = dad.curCharacter.startsWith('gf-') || dad.curCharacter == 'gf';
							var lastAlpha:Float = dad.alpha;
							dad.alpha = 0.00001;
							dad = dadMap.get(value2);
							if (!dad.curCharacter.startsWith('gf-') && dad.curCharacter != 'gf')
							{
								if (wasGf && gf != null)
								{
									gf.visible = true;
								}
							}
							else if (gf != null)
							{
								gf.visible = false;
							}
							dad.alpha = lastAlpha;
							iconP2.changeIcon(dad.healthIcon);
						}
						setOnScripts('dadName', dad.curCharacter);

					case 2:
						if (gf != null)
						{
							if (gf.curCharacter != value2)
							{
								if (!gfMap.exists(value2))
								{
									addCharacterToList(value2, charType);
								}

								var lastAlpha:Float = gf.alpha;
								gf.alpha = 0.00001;
								gf = gfMap.get(value2);
								gf.alpha = lastAlpha;
							}
							setOnScripts('gfName', gf.curCharacter);
						}
				}
				reloadHealthBarColors();

			case 'Change Scroll Speed':
				if (songSpeedType != "constant")
				{
					if (flValue1 == null)
						flValue1 = 1;
					if (flValue2 == null)
						flValue2 = 0;

					var newValue:Float = SONG.speed * ClientPrefs.getGameplaySetting('scrollspeed') * flValue1;
					if (flValue2 <= 0)
						songSpeed = newValue;
					else
						songSpeedTween = FlxTween.tween(this, {songSpeed: newValue}, flValue2 / playbackRate, {
							ease: FlxEase.linear,
							onComplete: function(twn:FlxTween)
							{
								songSpeedTween = null;
							}
						});
				}

			case 'Set Property':
				try
				{
					var split:Array<String> = value1.split('.');
					if (split.length > 1)
					{
						LuaUtils.setVarInArray(LuaUtils.getPropertyLoop(split), split[split.length - 1], value2);
					}
					else
					{
						LuaUtils.setVarInArray(this, value1, value2);
					}
				}
				catch (e:Dynamic)
				{
					var len:Int = e.message.indexOf('\n') + 1;
					if (len <= 0)
						len = e.message.length;
					#if (FEATURE_LUA || FEATURE_HSCRIPT)
					addTextToDebug('ERROR ("Set Property" Event) - ' + e.message.substr(0, len), FlxColor.RED);
					#else
					FlxG.log.warn('ERROR ("Set Property" Event) - ' + e.message.substr(0, len));
					#end
				}

			case 'Play Sound':
				if (flValue2 == null)
					flValue2 = 1;
				FlxG.sound.play(Paths.sound(value1), flValue2);

			case 'Set Camera Bopping':
				var val1:Float = Std.parseFloat(value1);
				var val2:Float = Std.parseFloat(value2);
				camZoomingMult = !Math.isNaN(val2) ? val2 : 1;
				camZoomingFrequency = !Math.isNaN(val1) ? val1 : 4;

			case 'Focus Camera': // P-slice focus camera event notes val1: char val2: x,y,dur,ease
				isCameraOnForcedPos = false;
				var keyValues:Array<String> = value2.split(",");
				if (keyValues.length != 4)
				{
					trace("INVALID EVENT VALUE");
					return;
				}
				var ease:String = keyValues.pop().toLowerCase();
				var floaties:Array<Float> = keyValues.map(s -> Std.parseFloat(s));
				if (findIndex(floaties, s -> Math.isNaN(s)) != -1)
				{
					trace("INVALID FLOATIES");
					return;
				}
				isCameraOnForcedPos = true;

				var targetX:Float = floaties[0];
				var targetY:Float = floaties[1];
				var duration:Float = floaties[2] * (Conductor.stepCrochet / 1000);
				switch (value1.toString().toLowerCase())
				{
					case "bf" | "0": {
						targetX += boyfriend.getMidpoint().x - 100 - boyfriend.cameraPosition[0] + boyfriendCameraOffset[0];
						targetY += boyfriend.getMidpoint().y - 100 + boyfriend.cameraPosition[1] + boyfriendCameraOffset[1];
					}
					case "dad" | "1": {
						targetX += dad.getMidpoint().x + 150 + dad.cameraPosition[0] + opponentCameraOffset[0];
						targetY += dad.getMidpoint().y - 100 + dad.cameraPosition[1] + opponentCameraOffset[1];
					}
					case "gf" | "2": {
						targetX += gf.getMidpoint().x + gf.cameraPosition[0] - girlfriendCameraOffset[0];
						targetY += gf.getMidpoint().y + gf.cameraPosition[1] - girlfriendCameraOffset[1];
					}
				}

				camTween?.cancel();
				camTween = null;

				if (ease == "classic" || ease == "instant")
				{
					camFollow.x = targetX;
					camFollow.y = targetY;
					if (ease == "instant")
						FlxG.camera.snapToTarget();
				}
				else
				{
					var easeFunc = psychlua.LuaUtils.getTweenEaseByString(ease);
					camTween = FlxTween.tween(camFollow, {x: targetX, y: targetY}, duration, {
						ease: easeFunc,
						onComplete: s ->
						{
							camTween = null;
						}
					});
				}

			case 'Zoom Camera': // P-slice zoom camera event notes val1: dur,zoom val2: ease
				var keyValues:Array<String> = value1.split(",");
				if (keyValues.length != 2)
				{
					trace("INVALID EVENT VALUE");
					return;
				}

				var floaties:Array<Float> = keyValues.map(s -> Std.parseFloat(s));
				if (findIndex(floaties, s -> Math.isNaN(s)) != -1)
				{
					trace("INVALID FLOATIES");
					return;
				}

				var targetZoom:Float = floaties[1];
				if (targetZoom <= 0)
				{
					trace("INVALID TARGET ZOOM: " + targetZoom);
					return;
				}

				if (zoomTween != null)
				{
					zoomTween.cancel();
					zoomTween = null;
				}

				var easeStr:String = (value2 != null) ? value2.toLowerCase() : "";
				if (easeStr == "classic" || easeStr == "instant")
				{
					defaultCamZoom = targetZoom;

					if (easeStr == "instant")
						FlxG.camera.zoom = targetZoom;

					return;
				}

				var easeFunc = psychlua.LuaUtils.getTweenEaseByString(value2);

				zoomTween = FlxTween.tween(this, {defaultCamZoom: targetZoom}, (Conductor.stepCrochet / 1000) * floaties[0], {
					onStart: (x) ->
					{
						camZooming = false;
						camZoomingDecay = 7;
					},
					ease: easeFunc,
					onComplete: (x) ->
					{
						defaultCamZoom = targetZoom;
						camZoomingDecay = 1;
						camZooming = true;
						zoomTween = null;
					}
				});
		}

		final args:Array<Dynamic> = [eventName, value1, value2, strumTime];
		stagesFunc(function(stage:BaseStage) stage.eventCalled(eventName, value1, value2, flValue1, flValue2, strumTime));
		callOnScripts('onEvent', args);
	}

	private static function findIndex<T>(array:Array<T>, predicate:T->Bool):Int
	{
		for (i in 0...array.length)
			if (predicate(array[i]))
				return i;

		return -1;
	}

	function moveCameraSection(?sec:Null<Int>):Void
	{
		sec ??= curSection;

		if (sec < 0)
			sec = 0;

		if (SONG.notes[sec] == null)
			return;

		if (gf != null && SONG.notes[sec].gfSection)
		{
			camFollow.setPosition(gf.getMidpoint().x, gf.getMidpoint().y);
			camFollow.x += gf.cameraPosition[0] + girlfriendCameraOffset[0];
			camFollow.y += gf.cameraPosition[1] + girlfriendCameraOffset[1];
			callOnScripts('onMoveCamera', ['gf']);
			return;
		}

		var isDad:Bool = (SONG.notes[sec].mustHitSection != true);
		moveCamera(isDad);
		callOnScripts('onMoveCamera', [isDad ? 'dad' : 'boyfriend']);
	}

	var cameraTwn:FlxTween;

	public function moveCamera(isDad:Bool)
	{
		if (isDad)
		{
			camFollow.setPosition(dad.getMidpoint().x + 150, dad.getMidpoint().y - 100);
			camFollow.x += dad.cameraPosition[0] + opponentCameraOffset[0];
			camFollow.y += dad.cameraPosition[1] + opponentCameraOffset[1];
		}
		else
		{
			camFollow.setPosition(boyfriend.getMidpoint().x - 100, boyfriend.getMidpoint().y - 100);
			camFollow.x -= boyfriend.cameraPosition[0] - boyfriendCameraOffset[0];
			camFollow.y += boyfriend.cameraPosition[1] + boyfriendCameraOffset[1];
		}
	}

	public function finishSong(?ignoreNoteOffset:Bool = false):Void
	{
		updateTime = false;
		FlxG.sound.music.volume = 0;

		vocals.volume = 0;
		vocals.pause();

		if (opponentVocals != null)
		{
			opponentVocals.volume = 0;
			opponentVocals.pause();
		}

		if (ClientPrefs.data.noteOffset <= 0 || ignoreNoteOffset)
			endCallback();
		else
			finishTimer = new FlxTimer().start(ClientPrefs.data.noteOffset / 1000, (_) -> endCallback());
	}

	public function showResults():Void
	{
		persistentUpdate = false;
		persistentDraw = true;
		paused = true;
		switchSubState(ResultsScreen);
	}

	public function endSong()
	{
		#if FEATURE_MOBILE_CONTROLS
		mobileControls.instance.visible = touchPad.visible = false;
		#end

		// Hand any still-pending input events to the results log, so the last press in each lane
		// isn't dropped from the hit graph.
		for (direction in 0...anas.length)
			flushAna(direction);
		// Should kill you if you tried to cheat
		if (!startingSong)
		{
			notes.forEach(function(daNote:Note)
			{
				if (daNote.strumTime < songLength - Conductor.safeZoneOffset)
				{
					health -= 0.05 * healthLoss;
				}
			});
			for (daNote in unspawnNotes)
			{
				if (daNote.strumTime < songLength - Conductor.safeZoneOffset)
				{
					health -= 0.05 * healthLoss;
				}
			}

			if (doDeathCheck())
			{
				return false;
			}
		}

		timeBar.visible = false;
		timeTxt.visible = false;
		canPause = false;
		endingSong = true;
		camZooming = false;
		inCutscene = false;
		updateTime = false;
		characterPlayingAsDad = false;

		deathCounter = 0;
		seenCutscene = false;

		var ret:Dynamic = callOnScripts('onEndSong', null, true);
		if (ret != ScriptResult.Stop && !transitioning)
		{
			var percent:Float = ratingPercent;
			if (Math.isNaN(percent))
				percent = 0;
			Highscore.saveScore(SONG.song, songScore, storyDifficulty, percent);

			playbackRate = 1;

			if (chartingMode)
			{
				openChartEditor();
				return false;
			}

			transitioning = true;
			showResults();
		}
		return true;
	}

	public function doTransitionAfterResults():Void
	{
		if (isStoryMode)
		{
			campaignScore += songScore;
			campaignMisses += songMisses;

			storyPlaylist.remove(storyPlaylist[0]);

			if (storyPlaylist.length <= 0)
			{
				Mods.loadTopMod();
				FlxG.sound.playMusic(Paths.music('freakyMenu'));
				#if FEATURE_DISCORD_RPC DiscordClient.resetClientID(); #end

				Funkin.switchState(StoryMenuState);

				if (!ClientPrefs.getGameplaySetting('practice') && !ClientPrefs.getGameplaySetting('botplay'))
				{
					StoryMenuState.weekCompleted.set(WeekData.weeksList[storyWeek], true);
					Highscore.saveWeekScore(WeekData.getWeekFileName(), campaignScore, storyDifficulty);

					FlxG.save.data.weekCompleted = StoryMenuState.weekCompleted;
					FlxG.save.flush();
				}
				changedDifficulty = false;
			}
			else
			{
				var difficulty:String = Difficulty.getFilePath();

				trace('LOADING NEXT SONG');
				trace(Paths.formatToSongPath(PlayState.storyPlaylist[0]) + difficulty);

				FlxTransitionableState.skipNextTransIn = true;
				FlxTransitionableState.skipNextTransOut = true;
				prevCamFollow = camFollow;

				PlayState.SONG = Song.loadFromJson(PlayState.storyPlaylist[0] + difficulty, PlayState.storyPlaylist[0]);
				FlxG.sound.music.stop();

				LoadingState.prepareToSong();
				LoadingState.loadAndSwitchState(PlayState, false, false);
			}
		}
		else
		{
			trace('WENT BACK TO FREEPLAY??');
			Mods.loadTopMod();
			#if FEATURE_DISCORD_RPC DiscordClient.resetClientID(); #end

			Funkin.switchState(FreeplayState);
			FlxG.sound.playMusic(Paths.music('freakyMenu'));
			changedDifficulty = false;
		}
	}

	public function KillNotes()
	{
		while (notes.length > 0)
		{
			var daNote:Note = notes.members[0];
			daNote.active = daNote.visible = false;
			invalidateNote(daNote);
		}
		unspawnNotes = [];
		eventNotes = [];
	}

	function popUpScore(?note:Note):Void
	{
		final noteDiffNoAbs:Float = note.strumTime - Conductor.songPosition + ClientPrefs.data.ratingOffset;
		final noteDiff:Float = Math.abs(noteDiffNoAbs);

		final daRating:Rating = switch (Rating.judgeNote(noteDiff))
		{
			case 'sick': ratingsData[0];
			case 'good': ratingsData[1];
			case 'bad': ratingsData[2];
			case 'shit': ratingsData[3];
			default: ratingsData[3];
		}

		switch (daRating.image)
		{
			case 'good': ++totalGood;
			case 'bad': ++totalBad;
			case 'shit': ++totalShit;
			default: ++totalSick;
		}

		totalNotesHit += daRating.ratingMod;
		note.ratingMod = daRating.ratingMod;

		if (!note.ratingDisabled)
			daRating.hits++;

		note.rating = daRating.name;

		if (daRating.noteSplash && !note.noteSplashData.disabled)
			spawnNoteSplashOnNote(note);

		if (!practiceMode && !cpuControlled && !note.ratingDisabled)
		{
			songScore += Rating.scoreNote(noteDiff);
			songHits++;
			totalPlayed++;
		}

		if (ClientPrefs.data.popUpRating)
		{
			final uiPrefix:String = stageUI != "normal" ? '${stageUI}UI/' : '';
			final uiSuffix:String = isPixelStage ? '-pixel' : '';
			final antialias:Bool = ClientPrefs.data.antialiasing && (stageUI == "normal" || !isPixelStage);
			final lifetime:Float = Conductor.crochet * 0.001 / playbackRate;
			final placement:Float = FlxG.width * 0.35;
			var yoRating:Float = 0;

			if(showRating)
			{
				final rating:Combo = comboGroup.recycle(Combo);
				rating.loadGraphic(Paths.image(uiPrefix + daRating.image + uiSuffix));
				rating.screenCenter();
				rating.alpha = 1;
				rating.x = placement - 40 + ClientPrefs.data.comboOffset[0];
				rating.y -= 60 + ClientPrefs.data.comboOffset[1];
				rating.acceleration.set(0, 550 * playbackRate * playbackRate);
				rating.velocity.set();
				rating.velocity.x -= FlxG.random.int(-10, 10) * playbackRate;
				rating.velocity.y -= FlxG.random.int(130, 160) * playbackRate;
				rating.antialiasing = antialias;
				rating.pixelPerfectRender = rating.pixelPerfectPosition = !antialias;
				rating.cameras = [camHUD];
				rating.lifeTime = lifetime;
				rating.setGraphicSize(Std.int(rating.width * (isPixelStage ? daPixelZoom * 0.85 : 0.7)));
				rating.updateHitbox();
				rating.ID = Std.int(Conductor.songPosition);
				yoRating = rating.y + rating.height;
			}

			if(showCombo)
			{
				final comboSpr:Combo = comboGroup.recycle(Combo);
				comboSpr.loadGraphic(Paths.image(uiPrefix + 'combo' + uiSuffix));
				comboSpr.screenCenter();
				comboSpr.x = placement + 40 + ClientPrefs.data.comboOffset[0];
				comboSpr.y += 50 + ClientPrefs.data.comboOffset[1];
				comboSpr.alpha = 1;
				comboSpr.acceleration.set(0, FlxG.random.int(200, 300) * playbackRate * playbackRate);
				comboSpr.velocity.set();
				comboSpr.velocity.x += FlxG.random.int(1, 10) * playbackRate;
				comboSpr.velocity.y -= FlxG.random.int(140, 160) * playbackRate;
				comboSpr.antialiasing = antialias;
				comboSpr.pixelPerfectRender = comboSpr.pixelPerfectPosition = !antialias;
				comboSpr.cameras = [camHUD];
				comboSpr.lifeTime = lifetime;
				comboSpr.setGraphicSize(Std.int(comboSpr.width * (isPixelStage ? daPixelZoom * 0.75 : 0.6)));
				comboSpr.updateHitbox();
				comboSpr.ID = Std.int(Conductor.songPosition);
			}

			if (ClientPrefs.data.showNoteTiming && showRating)
			{
				noteTimingRating.color = switch (daRating.name)
				{
					case 'sick': FlxColor.CYAN;
					case 'good': FlxColor.LIME;
					default: FlxColor.RED;
				}

				noteTimingRating.text = FlxMath.roundDecimal(noteDiffNoAbs / playbackRate, 3) + "ms";
				noteTimingRating.alpha = 1;
				noteTimingRating.exists = true;
				noteTimingRating.setPosition(placement + ClientPrefs.data.comboOffset[0] + 100, yoRating + (isPixelStage ? 60 : 0));
				noteTimingRatingTween?.cancel();

				noteTimingRatingTween = FlxTween.tween(noteTimingRating, {alpha: 0}, 0.2 / playbackRate, {
					startDelay: Conductor.crochet * 0.001 / playbackRate,
					onComplete: (_) -> noteTimingRating.exists = false,
					ease: isPixelStage ? EaseUtil.stepped(2) : null
				});
			}

			if (showComboNum)
			{
				final sepScore:String = '$combo';

				for (i in 0...sepScore.length)
				{
					final num:Int = Std.parseInt(sepScore.charAt(i));
					final numScore:Combo = comboGroup.recycle(Combo);
					numScore.loadGraphic(Paths.image(uiPrefix + 'num' + num + uiSuffix));
					numScore.screenCenter();
					numScore.alpha = 1;
					numScore.x = (placement - 30 + ClientPrefs.data.comboOffset[2]) - (sepScore.length * 43 / 2) + i * 43;
					numScore.y += 80 - ClientPrefs.data.comboOffset[3];
					numScore.setGraphicSize(Std.int(numScore.width * (isPixelStage ? daPixelZoom : 0.5)));
					numScore.updateHitbox();
					numScore.velocity.set(0, 0);
					numScore.velocity.x -= FlxG.random.float(-10, 10) * playbackRate;
					numScore.velocity.y -= FlxG.random.int(135, 155) * playbackRate;
					numScore.acceleration.set(0, FlxG.random.int(200, 300) * playbackRate * playbackRate);
					numScore.antialiasing = antialias;
					numScore.pixelPerfectRender = numScore.pixelPerfectPosition = !antialias;
					numScore.cameras = [camHUD];
					numScore.lifeTime = lifetime;
					numScore.ID = Std.int(Conductor.songPosition);
				}
			}

			comboGroup.sort((o:Int, a:Combo, b:Combo) -> return FlxSort.byValues(FlxSort.ASCENDING, a.ID, b.ID));
		}
	}

	public var strumsBlocked:Array<Bool> = [];

	private function onKeyPress(event:KeyboardEvent):Void
	{
		var eventKey:FlxKey = event.keyCode;
		var key:Int = getKeyFromEvent(keysArray, eventKey);

		if (!Funkin.controls.controllerMode)
		{
			#if debug
			// Prevents crash specifically on debug without needing to try catch shit
			@:privateAccess if (!FlxG.keys._keyListMap.exists(eventKey))
				return;
			#end

			if (FlxG.keys.checkStatus(eventKey, JUST_PRESSED))
				keyPressed(key);
		}
	}

	private function keyPressed(key:Int)
	{
		var char:Character = (!characterPlayingAsDad) ? boyfriend : dad;
		if (cpuControlled || paused || inCutscene || key < 0 || key >= playerStrums.length || !generatedMusic || endingSong || char.stunned)
			return;

		var ret:Dynamic = callOnScripts('onKeyPressPre', [key]);
		if (ret == ScriptResult.Stop)
			return;

		// obtain notes that the player can hit
		var plrInputNotes:Array<Note> = notes.members.filter(function(n:Note):Bool
		{
			var canHit:Bool = !strumsBlocked[n.noteData] && n.canBeHit && n.mustPress && !n.tooLate && !n.wasGoodHit && !n.blockHit;
			return n != null && canHit && !n.isSustainNote && n.noteData == key;
		});
		plrInputNotes.sort(sortHitNotes);

		var shouldMiss:Bool = !ClientPrefs.data.ghostTapping;

		if (plrInputNotes.length != 0) // slightly faster than doing "> 0" lol
		{
			var funnyNote:Note = plrInputNotes[0]; // front note

			if (plrInputNotes.length > 1)
			{
				var doubleNote:Note = plrInputNotes[1];

				if (doubleNote.noteData == funnyNote.noteData)
				{
					// if the note has a 0ms distance (is on top of the current note), kill it
					if (Math.abs(doubleNote.strumTime - funnyNote.strumTime) < 1.0)
						invalidateNote(doubleNote);
					else if (doubleNote.strumTime < funnyNote.strumTime)
					{
						// replace the note if its ahead of time (or at least ensure "doubleNote" is ahead)
						funnyNote = doubleNote;
					}
				}
			}
			goodNoteHit(funnyNote);
		}
		else if (shouldMiss)
		{
			callOnScripts('onGhostTap', [key]);
			noteMissPress(key);
		}

		var spr:StrumNote = playerStrums.members[key];
		if (strumsBlocked[key] != true && spr != null && spr.animation.curAnim.name != 'confirm')
		{
			spr.playAnim('pressed');
			spr.resetAnim = 0;
		}

		callOnScripts('onKeyPress', [key]);
	}

	public static function sortHitNotes(a:Note, b:Note):Int
	{
		if (a.lowPriority && !b.lowPriority)
			return 1;
		else if (!a.lowPriority && b.lowPriority)
			return -1;

		return FlxSort.byValues(FlxSort.ASCENDING, a.strumTime, b.strumTime);
	}

	private function onKeyRelease(event:KeyboardEvent):Void
	{
		var eventKey:FlxKey = event.keyCode;
		var key:Int = getKeyFromEvent(keysArray, eventKey);
		if (!Funkin.controls.controllerMode && key > -1)
			keyReleased(key);
	}

	private function keyReleased(key:Int)
	{
		if (cpuControlled || !startedCountdown || paused || key < 0 || key >= playerStrums.length)
			return;

		var ret:Dynamic = callOnScripts('onKeyReleasePre', [key]);
		if (ret == ScriptResult.Stop)
			return;

		var spr:StrumNote = playerStrums.members[key];
		if (spr != null)
		{
			spr.playAnim('static');
			spr.resetAnim = 0;
		}

		callOnScripts('onKeyRelease', [key]);
	}

	public static function getKeyFromEvent(arr:Array<String>, key:FlxKey):Int
	{
		if (key != NONE)
		{
			for (i in 0...arr.length)
			{
				var note:Array<FlxKey> = Controls.instance.keyboardBinds[arr[i]];
				for (noteKey in note)
					if (key == noteKey)
						return i;
			}
		}
		return -1;
	}

	#if FEATURE_MOBILE_CONTROLS
	// MobileInputID is an `enum abstract ... (Int)`, so these are plain integer comparisons. The old
	// checks went through `IDs.filter(id -> id.toString().startsWith("EXTRA"))`, which allocated a
	// closure and a result array and did a Map<MobileInputID, String> lookup plus a substring
	// compare per id - on the touch path that runs on every note tap and release.
	static inline function isExtraButtonID(id:MobileInputID):Bool
		return id == MobileInputID.EXTRA_1 || id == MobileInputID.EXTRA_2;

	static inline function isNoteButtonID(id:MobileInputID):Bool
		return id == MobileInputID.NOTE_LEFT || id == MobileInputID.NOTE_DOWN || id == MobileInputID.NOTE_UP || id == MobileInputID.NOTE_RIGHT;

	static function resolveButtonCode(button:TouchButton):Int
	{
		final ids:Array<MobileInputID> = button.IDs;
		for (id in ids)
			if (isExtraButtonID(id))
				return -1;

		if (ids.length == 0)
			return -1;

		return isNoteButtonID(ids[0]) ? ids[0] : (ids.length > 1 ? ids[1] : -1);
	}

	private function onButtonPress(button:TouchButton):Void
	{
		final buttonCode:Int = resolveButtonCode(button);
		if (buttonCode < 0)
			return;

		callOnScripts('onButtonPressPre', [buttonCode]);
		if (button.justPressed)
			keyPressed(buttonCode);
		callOnScripts('onButtonPress', [buttonCode]);
	}

	private function onButtonRelease(button:TouchButton):Void
	{
		final buttonCode:Int = resolveButtonCode(button);
		if (buttonCode < 0)
			return;

		callOnScripts('onButtonReleasePre', [buttonCode]);
		keyReleased(buttonCode);
		callOnScripts('onButtonRelease', [buttonCode]);
	}
	#end

	// Reused per-frame input buffers. These used to be three fresh arrays (plus a push per key)
	// allocated on every single frame of gameplay.
	final holdArray:Array<Bool> = [];
	final pressArray:Array<Bool> = [];
	final releaseArray:Array<Bool> = [];

	// Hold notes
	private function keysCheck():Void
	{
		// HOLDING
		final keyCount:Int = keysArray.length;
		if (holdArray.length != keyCount)
		{
			holdArray.resize(keyCount);
			pressArray.resize(keyCount);
			releaseArray.resize(keyCount);
		}

		var anyHold:Bool = false;
		var anyPress:Bool = false;
		var anyRelease:Bool = false;
		for (i in 0...keyCount)
		{
			final key:String = keysArray[i];
			final held:Bool = Funkin.controls.pressed(key);
			final pressed:Bool = Funkin.controls.justPressed(key);
			final released:Bool = Funkin.controls.justReleased(key);
			holdArray[i] = held;
			pressArray[i] = pressed;
			releaseArray[i] = released;
			anyHold = anyHold || held;
			anyPress = anyPress || pressed;
			anyRelease = anyRelease || released;

			if (pressed)
			{
				// Any ana still sitting in this lane never reached a hit or a miss - record it now
				// rather than leaving it to be re-pushed by every later note hit.
				flushAna(i);
				anas[i] = new Ana(Conductor.songPosition, null, false, "miss", i);
			}
		}

		// TO DO: Find a better way to handle controller inputs, this should work for now
		if (Funkin.controls.controllerMode && anyPress)
			for (i in 0...pressArray.length)
				if (pressArray[i] && strumsBlocked[i] != true)
					keyPressed(i);

		var char:Character = (!characterPlayingAsDad) ? boyfriend : dad;

		if (startedCountdown && !inCutscene && !char.stunned && generatedMusic)
		{
			if (notes.length > 0)
			{
				// `for (n in notes)` allocated a FlxTypedGroupIterator every frame. Walk `members`
				// directly instead, backwards so goodNoteHit() -> invalidateNote() can splice the
				// array without skipping entries.
				final noteMembers:Array<Note> = notes.members;
				var i:Int = noteMembers.length - 1;
				while (i >= 0)
				{
					if (i >= noteMembers.length)
					{
						i = noteMembers.length - 1;
						continue;
					}

					final n:Note = noteMembers[i];
					i--;

					var canHit:Bool = (n != null && !strumsBlocked[n.noteData] && n.canBeHit && n.mustPress && !n.tooLate && !n.wasGoodHit && !n.blockHit);

					if (guitarHeroSustains)
						canHit = canHit && n.parent != null && n.parent.wasGoodHit;

					if (canHit && n.isSustainNote)
					{
						var released:Bool = !holdArray[n.noteData];

						if (!released)
						{
							updateAna(n);
							goodNoteHit(n);
						}
					}
				}
			}

			if (!anyHold || endingSong)
				playerDance();

			for (i => hold in holdArray)
				if (hold)
					SustainSplash.showAtData(i);
				else
					SustainSplash.hideAtData(i);
		}

		// TO DO: Find a better way to handle controller inputs, this should work for now
		if ((Funkin.controls.controllerMode || strumsBlocked.contains(true)) && anyRelease)
			for (i in 0...releaseArray.length)
				if (releaseArray[i] || strumsBlocked[i] == true)
					keyReleased(i);
	}

	function noteMiss(daNote:Note):Void // You didn't hit the key and let it go offscreen, also used by Hurt Notes
	{
		// Dupe note remove.
		// Indexed reverse loop rather than `forEachAlive(closure)`: the closure allocated on every
		// miss, and invalidateNote() splices `notes.members` from underneath the iteration.
		if (daNote.mustPress)
		{
			var i:Int = notes.members.length - 1;
			while (i >= 0)
			{
				final note:Note = notes.members[i];
				i--;
				if (note == null || !note.exists || !note.alive || note == daNote)
					continue;
				if (daNote.noteData == note.noteData
					&& daNote.isSustainNote == note.isSustainNote
					&& Math.abs(daNote.strumTime - note.strumTime) < 1)
					invalidateNote(note);
			}
		}
		songSaveNotes.push([
			daNote != null ? daNote.strumTime : Conductor.songPosition,
			0,
			daNote.noteData,
			-(166 * Math.floor((ClientPrefs.data.safeFrames / 60) * 1000) / 166)
		]);
		songJudges.push("miss");

		final end:Note = daNote.isSustainNote ? daNote.parent.tail[daNote.parent.tail.length - 1] : daNote.tail[daNote.tail.length - 1];
		if (end != null && end.extraData['holdSplash'] != null)
			end.extraData['holdSplash'].visible = false;

		noteMissCommon(daNote.noteData, daNote);
		final args:Array<Dynamic> = [
			notes.members.indexOf(daNote),
			daNote.noteData,
			daNote.noteType,
			daNote.isSustainNote
		];
		callOnLuas('noteMiss', args);
		callOnHScript('noteMiss', [daNote]);
	}

	// `Paths.soundRandom()` resolves a path (mod lookup + FileSystem.exists) every time it's called.
	// There are only three miss sounds, so resolve them once and pick from the cache.
	final _missSounds:Array<Null<openfl.media.Sound>> = [null, null, null];

	function getMissSound():Null<openfl.media.Sound>
	{
		final index:Int = FlxG.random.int(0, 2);
		var snd:Null<openfl.media.Sound> = _missSounds[index];
		if (snd == null)
		{
			snd = Paths.sound('missnote' + (index + 1));
			_missSounds[index] = snd;
		}
		return snd;
	}

	function noteMissPress(direction:Int = 1):Void // You pressed a key when there was no notes to press for this key
	{
		if (ClientPrefs.data.ghostTapping)
			return; // fuck it we ball

		noteMissCommon(direction);
		callOnScripts('noteMissPress', [direction]);
	}

	function noteMissCommon(direction:Int, note:Note = null)
	{
		// score and data
		var subtract:Float = 0.05;
		if (note != null)
			subtract = note.missHealth;

		// GUITAR HERO SUSTAIN CHECK LOL!!!!
		if (note != null && guitarHeroSustains && note.parent == null)
		{
			if (note.tail.length > 0)
			{
				note.alpha = 0.35;
				for (childNote in note.tail)
				{
					childNote.alpha = note.alpha;
					childNote.missed = true;
					childNote.canBeHit = false;
					childNote.ignoreNote = true;
					childNote.tooLate = true;
				}
				note.missed = true;
				note.canBeHit = false;

				// subtract += 0.385; // you take more damage if playing with this gameplay changer enabled.
				// i mean its fair :p -Crow
				subtract *= note.tail.length + 1;
				// i think it would be fair if damage multiplied based on how long the sustain is -Tahir
			}

			if (note.missed)
				return;
		}
		if (note != null && guitarHeroSustains && note.parent != null && note.isSustainNote)
		{
			if (note.missed)
				return;

			var parentNote:Note = note.parent;
			if (parentNote.wasGoodHit && parentNote.tail.length > 0)
			{
				for (child in parentNote.tail)
					if (child != note)
					{
						child.missed = true;
						child.canBeHit = false;
						child.ignoreNote = true;
						child.tooLate = true;
					}
			}
		}

		if (note.mustPress)
		{
			missnoteSound = FlxG.sound.play(getMissSound(), FlxG.random.float(0.1, 0.2));
			if (missnoteSound != null)
				missnoteSound.pitch = playbackRate;
		}

		if (instakillOnMiss)
		{
			vocals.volume = 0;

			if (opponentVocals != null)
				opponentVocals.volume = 0;

			doDeathCheck(true);
		}

		flushAna(direction);

		var lastCombo:Int = combo;
		combo = 0;

		health -= subtract * healthLoss;
		if (!practiceMode)
			songScore -= (note.isSustainNote) ? Rating.SUSTAIN_MISS_SCORE : Rating.MISS_SCORE;
		if (!endingSong)
			songMisses++;
		totalPlayed++;

		// play character anims
		var char:Character = (!characterPlayingAsDad) ? boyfriend : dad;
		if ((note != null && note.gfNote) || (SONG.notes[curSection] != null && SONG.notes[curSection].gfSection))
			char = gf;

		if (char != null && (note == null || !note.noMissAnimation) && char.hasMissAnimations)
		{
			var suffix:String = '';
			if (note != null)
				suffix = note.animSuffix;

			var animToPlay:String = singAnimations[Std.int(Math.abs(Math.min(singAnimations.length - 1, direction)))] + 'miss' + suffix;
			char.playAnim(animToPlay, true);

			if (char != gf && lastCombo > 5 && gf != null && gf.animOffsets.exists('sad'))
			{
				gf.playAnim('sad');
				gf.specialAnim = true;
			}
		}

		if (char == dad && opponentVocals != null)
			opponentVocals.volume = 0;
		else
			vocals.volume = 0;
	}

	function opponentNoteHit(note:Note):Void
	{
		final noteIndex:Int = notes.members.indexOf(note);
		final args:Array<Dynamic> = [
			noteIndex,
			Math.abs(note.noteData),
			note.noteType,
			note.isSustainNote
		];
		callOnLuas('opponentNoteHitPre', args);
		callOnHScript('opponentNoteHitPre', [note]);

		camZooming = true;

		var char:Character = (!characterPlayingAsDad) ? dad : boyfriend;

		if (note.noteType == 'Hey!' && char.animOffsets.exists('hey'))
		{
			char.playAnim('hey', true);
			char.specialAnim = true;
			char.heyTimer = 0.6;
		}
		else if (!note.noAnimation)
		{
			var altAnim:String = note.animSuffix;

			if (SONG.notes[curSection] != null)
				if (SONG.notes[curSection].altAnim && !SONG.notes[curSection].gfSection)
					altAnim = '-alt';

			var animToPlay:String = singAnimations[Std.int(Math.abs(Math.min(singAnimations.length - 1, note.noteData)))] + altAnim;
			if (note.gfNote)
				char = gf;

			if (char != null)
			{
				char.playAnim(animToPlay, true);
				char.holdTimer = 0;
			}
		}

		if (char == dad && opponentVocals != null)
			opponentVocals.volume = 1;
		else
			vocals.volume = 1;

		strumPlayAnim(true, Std.int(Math.abs(note.noteData)), Conductor.stepCrochet * 1.25 / 1000 / playbackRate);
		note.hitByOpponent = true;

		final args2:Array<Dynamic> = [
			noteIndex,
			Math.abs(note.noteData),
			note.noteType,
			note.isSustainNote
		];
		callOnLuas('opponentNoteHit', args2);
		callOnHScript('opponentNoteHit', [note]);

		spawnHoldSplashOnNote(note);

		if (!note.isSustainNote)
			invalidateNote(note);
	}

	public function goodNoteHit(note:Note):Void
	{
		if (note.wasGoodHit)
			return;
		if (cpuControlled && note.ignoreNote)
			return;

		if (!note.noMissAnimation)
		{
			switch (note.noteType)
			{
				case 'Hurt Note': // Hurt note
					if (boyfriend.animOffsets.exists('hurt'))
					{
						boyfriend.playAnim('hurt', true);
						boyfriend.specialAnim = true;
					}
			}
		}

		var isSus:Bool = note.isSustainNote; // GET OUT OF MY HEAD, GET OUT OF MY HEAD, GET OUT OF MY HEAD
		var leData:Int = Math.round(Math.abs(note.noteData));
		var leType:String = note.noteType;
		var char:Character = (!characterPlayingAsDad) ? boyfriend : dad;

		// `notes.members.indexOf()` is a linear scan of every spawned note, and this used to run
		// twice per hit (once for the "Pre" event, once for the main one). Resolve it once.
		final noteIndex:Int = notes.members.indexOf(note);

		final args:Array<Dynamic> = [noteIndex, leData, leType, isSus];
		callOnLuas('goodNoteHitPre', args);
		callOnHScript('goodNoteHitPre', [note]);

		note.wasGoodHit = true;

		if (ClientPrefs.data.hitsoundVolume > 0 && !note.hitsoundDisabled)
		{
			hitsoundSound = FlxG.sound.play(Note.getHitsound(note.hitsound), ClientPrefs.data.hitsoundVolume);
			if (hitsoundSound != null)
				hitsoundSound.pitch = playbackRate;
		}

		if (note.hitCausesMiss)
		{
			noteMiss(note);
			if (!note.noteSplashData.disabled && !note.isSustainNote)
				spawnNoteSplashOnNote(note);
			if (!note.isSustainNote)
				invalidateNote(note);
			return;
		}

		if (!note.noAnimation)
		{
			var animToPlay:String = singAnimations[Std.int(Math.abs(Math.min(singAnimations.length - 1, note.noteData)))];

			var animCheck:String = 'hey';
			if (note.gfNote)
			{
				char = gf;
				animCheck = 'cheer';
			}

			if (char != null)
			{
				char.playAnim(animToPlay + note.animSuffix, true);
				char.holdTimer = 0;

				if (note.noteType == 'Hey!')
				{
					if (char.animOffsets.exists(animCheck))
					{
						char.playAnim(animCheck, true);
						char.specialAnim = true;
						char.heyTimer = 0.6;
					}
				}
			}
		}

		if (!cpuControlled)
		{
			var spr = playerStrums.members[note.noteData];
			if (spr != null)
				spr.playAnim('confirm', true);
		}
		else
			strumPlayAnim(false, Std.int(Math.abs(note.noteData)), Conductor.stepCrochet * 1.25 / 1000 / playbackRate);

		if (char == dad && opponentVocals != null)
			opponentVocals.volume = 1;
		else
			vocals.volume = 1;

		var noteDiff:Float = Math.abs(note.strumTime - Conductor.songPosition + ClientPrefs.data.ratingOffset);
		var array = [note.strumTime, note.sustainLength, note.noteData, noteDiff];
		if (note.isSustainNote)
			array[1] = -1;

		if (!note.isSustainNote)
		{
			++combo;
			if (maxCombo < combo)
				++maxCombo;
			//if (combo > 9999)
			//	combo = 9999;
			popUpScore(note);

			// This used to push *every* live ana on every tap hit, so `anaArray` grew by up to four
			// entries per note and filled up with duplicates - unbounded growth over a song, and a
			// wrong results graph. Hand over just this lane's input event.
			flushAna(note.noteData);
			songSaveNotes.push(array);
			songJudges.push(note.rating);
		}
		var gainHealth:Bool = true; // prevent health gain, *if* sustains are treated as a singular note
		if (guitarHeroSustains && note.isSustainNote)
			gainHealth = false;
		if (gainHealth)
			health += note.hitHealth * healthGain;

		if (note.isSustainNote && !cpuControlled)
			songScore += Rating.SUSTAIN_SCORE;

		final args:Array<Dynamic> = [noteIndex, leData, leType, isSus];
		callOnLuas('goodNoteHit', args);
		callOnHScript('goodNoteHit', [note]);

		if (!note.isSustainNote && !cpuControlled)
			doScoreBop();

		spawnHoldSplashOnNote(note);

		if (!note.isSustainNote)
			invalidateNote(note);
	}

	public function invalidateNote(note:Note):Void
	{
		if (!ClientPrefs.data.lowQuality || !ClientPrefs.data.popUpRating || !cpuControlled)
			note.kill();
		notes.remove(note, true);
		note.recycle();
	}

	public function spawnHoldSplashOnNote(note:Note)
	{
		if (note == null)
			return;

		if (note.tail.length > 0 && !note.isSustainNote)
		{
			var lastSustain:Note = note.tail[note.tail.length - 1];
			var strumNote:StrumNote = note.mustPress ? playerStrums.members[note.noteData] : opponentStrums.members[note.noteData];
			SustainSplash.generateSustainSplash(strumNote, lastSustain.strumTime, note.mustPress);
		}
		else if(note.isSustainNote && note.parent != null && note.parent.tail.length > 0 && !SustainSplash.hasSplashAtData(note.noteData, note.mustPress))
		{
			var lastSustain:Note = note.parent.tail[note.parent.tail.length - 1];
			var strumNote:StrumNote = note.mustPress ? playerStrums.members[note.noteData] : opponentStrums.members[note.noteData];
			SustainSplash.generateSustainSplash(strumNote, lastSustain.strumTime, note.mustPress);
		}
	}

	public function spawnNoteSplashOnNote(note:Note)
	{
		if (note != null)
		{
			var strum:StrumNote = playerStrums.members[note.noteData];
			if (strum != null)
				spawnNoteSplash(strum.x, strum.y, note.noteData, note);
		}
	}

	public function spawnNoteSplash(x:Float, y:Float, data:Int, ?note:Note = null)
	{
		var splash:NoteSplash = grpNoteSplashes.recycle(NoteSplash);
		splash.setupNoteSplash(x, y, data, note);
		grpNoteSplashes.add(splash);
	}

	override function destroy()
	{
		instance = null;

		FlxG.stage.removeEventListener(KeyboardEvent.KEY_DOWN, onKeyPress);
		FlxG.stage.removeEventListener(KeyboardEvent.KEY_UP, onKeyRelease);

		FlxG.animationTimeScale = 1;

		NoteSplash.mainGroup = null;
		NoteSplash.usePixelTextures = StrumNote.usePixelTextures = Note.usePixelTextures = null;
		Note.globalRgbShaders = [];
		Note.globalColorSwaps = [];
		SustainSplash.close();
		backend.NoteTypesConfig.clearNoteTypesData();

		@:privateAccess
		FlxG.game._filters = [];
		camGame.filters = camHUD.filters = camOther.filters = [];

		#if (target.threaded)
		shutdownThread = true;
		FlxG.signals.preUpdate.remove(checkForResync);
		#end

		FlxG.sound.music.pitch = 1;
		vocals = FlxDestroyUtil.destroy(vocals);
		opponentVocals = FlxDestroyUtil.destroy(opponentVocals);

		#if FEATURE_VIDEOS
		videoCutscene = FlxDestroyUtil.destroy(videoCutscene);
		#end

		super.destroy();
	}

	var lastStepHit:Int = -1;

	override function stepHit()
	{
		if (SONG.needsVoices && FlxG.sound.music.time >= -ClientPrefs.data.noteOffset)
		{
			final timeSub:Float = Conductor.songPosition - Conductor.offset;
			#if mobile
			final syncTime:Float = 75 * playbackRate;
			#else
			final syncTime:Float = 20 * playbackRate;
			#end

			if (Math.abs(FlxG.sound.music.time - timeSub) > syncTime || Math.abs(vocals.time - timeSub) > syncTime || (opponentVocals != null && opponentVocals.playing && Math.abs(opponentVocals.time - timeSub) > syncTime))
				resyncVocals();
		}

		super.stepHit();

		if (curStep == lastStepHit)
		{
			return;
		}

		lastStepHit = curStep;
	}

	var lastBeatHit:Int = -1;

	override function beatHit()
	{
		if (lastBeatHit >= curBeat)
		{
			// trace('BEAT HIT: ' + curBeat + ', LAST HIT: ' + lastBeatHit);
			return;
		}

		if (camZooming && FlxG.camera.zoom < 1.35 && ClientPrefs.data.camZooms && (curBeat % camZoomingFrequency) == 0)
		{
			FlxG.camera.zoom += 0.015 * camZoomingMult;
			camHUD.zoom += 0.03 * camZoomingMult;
		}

		if (generatedMusic)
			notes.sort(FlxSort.byY, ClientPrefs.data.downScroll ? FlxSort.ASCENDING : FlxSort.DESCENDING);

		iconP1.scale.set(1.2, 1.2);
		iconP2.scale.set(1.2, 1.2);

		iconP1.updateHitbox();
		iconP2.updateHitbox();

		characterBopper(curBeat);

		super.beatHit();
		lastBeatHit = curBeat;
	}

	public function characterBopper(beat:Int):Void
	{
		var charBf:Character = (!characterPlayingAsDad) ? boyfriend : dad;
		var charDad:Character = (!characterPlayingAsDad) ? dad : boyfriend;
		if (gf != null
			&& beat % Math.round(gfSpeed * gf.danceEveryNumBeats) == 0
			&& !gf.getAnimationName().startsWith('sing')
			&& !gf.stunned)
			gf.dance();
		if (boyfriend != null
			&& beat % charBf.danceEveryNumBeats == 0
			&& !boyfriend.getAnimationName().startsWith('sing')
			&& !charBf.stunned)
			boyfriend.dance();
		if (dad != null && beat % charDad.danceEveryNumBeats == 0 && !dad.getAnimationName().startsWith('sing') && !charDad.stunned)
			dad.dance();
	}

	public function playerDance():Void
	{
		var char:Character = (!characterPlayingAsDad) ? boyfriend : dad;
		var anim:String = char.getAnimationName();
		if (char.holdTimer > Conductor.stepCrochet * (0.0011 / FlxG.sound.music.pitch) * char.singDuration && anim.startsWith('sing')
			&& !anim.endsWith('miss'))
			char.dance();
	}

	override function sectionHit()
	{
		if (SONG.notes[curSection] != null)
		{
			if (generatedMusic && !endingSong && !isCameraOnForcedPos)
				moveCameraSection();

			final vsliceCondition:Bool = (curBeat % camZoomingFrequency) == 0;
			if (camZooming && FlxG.camera.zoom < 1.35 && ClientPrefs.data.camZooms && !vsliceCondition)
			{
				FlxG.camera.zoom += 0.015 * camZoomingMult;
				camHUD.zoom += 0.03 * camZoomingMult;
			}

			if (SONG.notes[curSection].changeBPM)
			{
				Conductor.bpm = SONG.notes[curSection].bpm;
				setOnScripts('curBpm', Conductor.bpm);
				setOnScripts('crochet', Conductor.crochet);
				setOnScripts('stepCrochet', Conductor.stepCrochet);
			}
			setOnScripts('mustHitSection', SONG.notes[curSection].mustHitSection);
			setOnScripts('altAnim', SONG.notes[curSection].altAnim);
			setOnScripts('gfSection', SONG.notes[curSection].gfSection);
		}
		super.sectionHit();
	}

	function strumPlayAnim(isDad:Bool, id:Int, time:Float)
	{
		var spr:StrumNote = null;
		if (isDad)
		{
			spr = opponentStrums.members[id];
		}
		else
		{
			spr = playerStrums.members[id];
		}

		if (spr != null)
		{
			spr.playAnim('confirm', true);
			spr.resetAnim = time;
		}
	}

	public var ratingName:String = '?';
	public var ratingPercent:Float;
	public var ratingFC:String;

	public function recalculateRating()
	{
		setOnScripts('score', songScore);
		setOnScripts('misses', songMisses);
		setOnScripts('hits', songHits);
		setOnScripts('combo', combo);

		var ret:Dynamic = callOnScripts('onRecalculateRating', null, true);
		if (ret != ScriptResult.Stop)
		{
			ratingName = '?';
			if (totalPlayed != 0) // Prevent divide by 0
			{
				// Rating Percent
				ratingPercent = Math.min(1, Math.max(0, totalNotesHit / totalPlayed));
				// trace((totalNotesHit / totalPlayed) + ', Total: ' + totalPlayed + ', notes hit: ' + totalNotesHit);

				// Rating Name
				ratingName = ratingStuff[ratingStuff.length - 1][0]; // Uses last string
				if (ratingPercent < 1)
					for (i in 0...ratingStuff.length - 1)
						if (ratingPercent < ratingStuff[i][1])
						{
							ratingName = ratingStuff[i][0];
							break;
						}
			}
			fullComboFunction();
		}
		updateScore(); // score will only update after rating is calculated, if it's a badHit, it shouldn't bounce
		setOnScripts('rating', ratingPercent);
		setOnScripts('ratingName', ratingName);
		setOnScripts('ratingFC', ratingFC);
	}

	public var runtimeShaders:Map<String, Array<String>> = new Map<String, Array<String>>();

	public function createRuntimeShader(name:String):FlxRuntimeShader
	{
		if (!ClientPrefs.data.shaders)
			return new FlxRuntimeShader();

		if (!runtimeShaders.exists(name) && !initLuaShader(name))
		{
			FlxG.log.warn('Shader $name is missing!');
			return new FlxRuntimeShader();
		}

		var arr:Array<String> = runtimeShaders.get(name);
		return new FlxRuntimeShader(arr[0], arr[1]);
	}

	public function initLuaShader(name:String):Bool
	{
		if (!ClientPrefs.data.shaders)
			return false;

		if (runtimeShaders.exists(name))
		{
			FlxG.log.warn('Shader $name was already initialized!');
			return true;
		}

		var folders:Array<String> = [];
		#if FEATURE_MODS
		if (Mods.currentModDirectory != null && Mods.currentModDirectory.length > 0)
			folders.push(Paths.mods(Mods.currentModDirectory + '/shaders/'));
		for (mod in Mods.getGlobalMods())
			folders.push(Paths.mods(mod + '/shaders/'));
		#end
		folders.push(Paths.getSharedPath('shaders/'));

		for (folder in folders)
		{
			if (!FileSystem.exists(folder))
				continue;

			var fragPath = folder + name + '.frag';
			var vertPath = folder + name + '.vert';

			var frag:String = null;
			var vert:String = null;
			var found:Bool = false;

			if (FileSystem.exists(fragPath))
			{
				frag = File.getContent(fragPath);
				found = true;
			}

			if (FileSystem.exists(vertPath))
			{
				vert = File.getContent(vertPath);
				found = true;
			}

			if (found)
			{
				runtimeShaders.set(name, [frag, vert]);
				return true;
			}
		}

		#if (FEATURE_LUA || FEATURE_HSCRIPT)
		addTextToDebug('Missing shader $name .frag AND .vert files!', FlxColor.RED);
		#else
		FlxG.log.warn('Missing shader $name .frag AND .vert files!');
		#end

		return false;
	}

	public function changeNoteSkin(player:Bool, skin:String)
	{
		if (!player)
		{
			if (skin != null || skin != '')
			{
				noteSkin1 = skin;
				opponentStrums.forEachExists(function(strumNote:StrumNote)
				{
					strumNote.texture = skin;
				});
				notes.forEachExists(function(note:Note)
				{
					if (!note.mustPress && allowedNotes.contains(note.noteType))
						note.texture = skin;
				});
			}
			#if (FEATURE_LUA || FEATURE_HSCRIPT)
			else
				addTextToDebug("ERROR!! couldn't change opponent note skin because the inserted value is null.", FlxColor.RED);
			#end
		}
		if (player)
		{
			if (skin != null || skin != '')
			{
				noteSkin = skin;
				playerStrums.forEachExists(function(strumNote:StrumNote)
				{
					strumNote.texture = skin;
				});
				notes.forEachExists(function(note:Note)
				{
					if (note.mustPress && allowedNotes.contains(note.noteType))
						note.texture = skin;
				});
			}
			#if (FEATURE_LUA || FEATURE_HSCRIPT)
			else
				addTextToDebug("ERROR!! couldn't change player note skin because the inserted value is null.", FlxColor.RED);
			#end
		}
	}

	public function changeSustainSplashSkin(player:Bool, skin:String)
	{
		if (skin == null || skin == '')
		{
			#if (FEATURE_LUA || FEATURE_HSCRIPT)
			addTextToDebug("ERROR!! couldn't change sustain splash skin because the inserted value is null.", FlxColor.RED);
			#end
			return;
		}

		if (player)
			SustainSplash.playerTexture = skin;
		else
			SustainSplash.opponentTexture = skin;

		@:privateAccess
		SustainSplash.mainGroup.forEachExists((splash:SustainSplash) ->
		{
			if (splash.mustPress == player)
				splash.reloadSustainSplash(SustainSplash.getTextureNameFromData(splash.noteData, splash.mustPress));
		});
	}

	public static function judgeNote(noteDiff:Float)
	{
		var diff = Math.abs(noteDiff);
		var timingWindows = [ClientPrefs.data.badWindow, ClientPrefs.data.goodWindow, ClientPrefs.data.sickWindow];
		for (index in 0...timingWindows.length) // based on 4 timing windows, will break with anything else
		{
			var time = timingWindows[index];
			var nextTime = index + 1 > timingWindows.length - 1 ? 0 : timingWindows[index + 1];
			if (diff < time && diff >= nextTime)
			{
				switch (index)
				{
					case 0: // bad
						return "bad";
					case 1: // good
						return "good";
					case 2: // sick
						return "sick";
				}
			}
		}
		return "good";
	}

	#if (target.threaded)
	function checkForResync()
	{
		if (endingSong || paused || shutdownThread)
			return;

		syncMutex.acquire();
		try
		{
			if (requiresSyncing)
			{
				requiresSyncing = false;
				setSongTime(lastCorrectSongPos);
			}

			gameFroze = false;
		}
		catch (e:Dynamic)
		{
			trace('Error during resync: $e');
		}
		syncMutex.release();
	}

	public function runSongSyncThread()
	{
		if (syncMutex == null)
			syncMutex = new Mutex();

		Thread.create(function()
		{
			try
			{
				while (!endingSong && !paused && !shutdownThread)
				{
					syncMutex.acquire();
					var shouldContinue:Bool = !requiresSyncing;
					syncMutex.release();

					if (!shouldContinue)
					{
						Sys.sleep(0.01);
						continue;
					}

					syncMutex.acquire();
					gameFroze = true;
					syncMutex.release();

					Sys.sleep(0.25);

					syncMutex.acquire();
					if (gameFroze && !shutdownThread)
					{
						lastCorrectSongPos = Conductor.songPosition;
						requiresSyncing = true;
					}
					syncMutex.release();
				}
			}
			catch (e:Dynamic)
			{
				trace('Song sync thread error: $e');
			}
		});

		if (!FlxG.signals.preUpdate.has(checkForResync))
			FlxG.signals.preUpdate.add(checkForResync);
	}
	#end
}


class Combo extends FlxSprite
{
	public var lifeTime:Float = 0;
}